package panda.gl.shapes

uses panda.gl.Point

================================================================================
Represents an axis-aligned ellipse.
================================================================================
class Ellipse : Immutable (Shape) {
	var x:Real
	var y:Real
	var width:Real
	var height:Real

    @invariant(width >= 0)
    @invariant(height >= 0)

	constructor(x:Real, y:Real, width:Real, height:Real) {
		self.x := x
		self.y := y
		self.width := width
		self.height := height
	}

    @override
    function path():Array<PathSegment> {
        constant CONTROL_FACTOR := 4 * (2.sqrt - 1) / 6
        def cx := width * CONTROL_FACTOR
        def cy := height * CONTROL_FACTOR
        return [new PathSegmentMove(new Point(x, y + height / 2)),
                new PathSegmentCurve(new Point(x + width / 2, y),
                    new Point(x, y + height / 2 - cy),
                    new Point(x + width / 2 - cx, y)),
                new PathSegmentCurve(new Point(x + width, y + height / 2),
                    new Point(x + width / 2 + cx, y),
                    new Point(x + width, y + height / 2 - cy)),
                new PathSegmentCurve(new Point(x + width / 2, y + height),
                    new Point(x + width, y + height / 2 + cy),
                    new Point(x + width / 2 + cx, y + height)),
                new PathSegmentCurve(new Point(x, y + height / 2),
                    new Point(x + width / 2 - cx, y + height),
                    new Point(x, y + height / 2 + cy))]
    }

    @override
    function format(fmt:String):String {
        return "Ellipse[\{x}, \{y}, \{width} x \{height}]"
    }
}