package panda.gl.shapes

uses panda.gl.Point

================================================================================
Represents an axis-aligned ellipse.
================================================================================
class Ellipse : Immutable (Shape) {
	def x:Real
	def y:Real
	def width:Real
	def height:Real

    @invariant(width >= 0)
    @invariant(height >= 0)

	init(x:Real, y:Real, width:Real, height:Real) {
		self.x := x
		self.y := y
		self.width := width
		self.height := height
	}

    @override
    function path():ListView<PathSegment> {
        constant CONTROL_FACTOR := 4 * (2.sqrt - 1) / 6
        def cx := width * CONTROL_FACTOR
        def cy := height * CONTROL_FACTOR
        return [PathSegmentMove(Point(x, y + height / 2)),
                PathSegmentCurve(Point(x + width / 2, y),
                    Point(x, y + height / 2 - cy),
                    Point(x + width / 2 - cx, y)),
                PathSegmentCurve(Point(x + width, y + height / 2),
                    Point(x + width / 2 + cx, y),
                    Point(x + width, y + height / 2 - cy)),
                PathSegmentCurve(Point(x + width / 2, y + height),
                    Point(x + width, y + height / 2 + cy),
                    Point(x + width / 2 + cx, y + height)),
                PathSegmentCurve(Point(x, y + height / 2),
                    Point(x + width / 2 - cx, y + height),
                    Point(x, y + height / 2 + cy))]
    }

    @override
    function format(fmt:String):String {
        return "Ellipse[\{x}, \{y}, \{width} x \{height}]"
    }
}