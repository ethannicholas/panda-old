package panda.gl.shapes

uses panda.gl.Point

================================================================================
Represents an axis-aligned rectangle.
================================================================================
class Rectangle : Immutable (Shape) {
	def x:Real
	def y:Real
	def width:Real
	def height:Real

    @invariant(width >= 0)
    @invariant(height >= 0)

	init(x:Real, y:Real, width:Real, height:Real) {
		self.x := x
		self.y := y
		self.width := width
		self.height := height
	}

	============================================================================
	Returns `true` if there is any intersection between the two rectangles.
	============================================================================
    function intersects(r:Rectangle):Bit {
        return x < r.x + r.width & x + width > r.x &
                y < r.y + r.height & y + height > r.y
    }

    function contains(p:Point):Bit {
        return p.x >= x & p.x < x + width & p.y >= y & p.y <= y + height
    }

    @override
    function path():ListView<PathSegment> {
        return [PathSegmentMove(Point(x, y)),
                PathSegmentLine(Point(x + width, y)),
                PathSegmentLine(Point(x + width, y + height)),
                PathSegmentLine(Point(x, y + height)),
                PathSegmentLine(Point(x, y))]
    }

    @override
    function convert():String {
        return "[\{x}, \{y}, \{width} x \{height}]"
    }
}