package panda.gl.shapes

uses panda.gl.Point

================================================================================
Represents an axis-aligned rectangle.
================================================================================
class Rectangle : Immutable (Shape) {
	def x:Real
	def y:Real
	def width:Real
	def height:Real

    @invariant(width >= 0)
    @invariant(height >= 0)

	constructor(x:Real, y:Real, width:Real, height:Real) {
		self.x := x
		self.y := y
		self.width := width
		self.height := height
	}

	============================================================================
	Returns `true` if there is any intersection between the two rectangles.
	============================================================================
    function intersects(r->>Rectangle):Bit {
        return x < r.x + r.width & x + width > r.x &
                y < r.y + r.height & y + height > r.y
    }

    @override
    function path():Array<PathSegment> {
        return [new PathSegmentMove(new Point(x, y)),
                new PathSegmentLine(new Point(x + width, y)),
                new PathSegmentLine(new Point(x + width, y + height)),
                new PathSegmentLine(new Point(x, y + height)),
                new PathSegmentLine(new Point(x, y))]
    }

    @override
    function format(fmt:String):String {
        return "[\{x}, \{y}, \{width} x \{height}]"
    }
}