package panda.math

================================================================================
A fast pseudorandom number generator using a XorShift+ algorithm with a period 
of 2^128 - 1.

This generator is guaranteed to always produce the same random sequence when
started from the same seed, even across different environments and versions of
Panda.
================================================================================
class XorShift128Plus (Random) {
    ============================================================================
    The current state.
    ============================================================================
    @private 
    var state1:UInt64
    @private 
    var state2:UInt64
    
    ============================================================================
    Creates a new, automatically-seeded `XorShift128Plus`.
    ============================================================================
    init() {
        init(Random.int64())
    }
    
    ============================================================================
    Creates a new `XorShift128Plus` with a specific seed. All `XorShift128Plus` 
    instances created with the same seed will return the same sequence of 
    pseudorandom numbers.
    
    @param seed the seed to use
    ============================================================================
    init(seed:Int64) {
        if seed = 0 {
            -- XorShift seed cannot be zero, replace with an arbitrarily-chosen
            -- value
            self.state1 := 12345678
        }
        else {
            self.state1:= seed.convert()->UInt64
        }
        self.state2 := !!self.state1
    }
    
    @override
    @math(overflow)
    method int64():Int64 {
        def s1 := state2
        var s2 := state1
        state1 := s1
        s2 ~~= s2 << 23
        s2 ~~= s2 >> 17
        s2 ~~= s1
        s2 ~~= s1 >> 26
        state2 := s2
        return state2.convert()
    }
}