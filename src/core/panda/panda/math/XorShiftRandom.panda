package panda.math

================================================================================
A fast pseudorandom number generator using a XorShift* algorithm with a period 
of 2^64 - 1.

This generator is guaranteed to always produce the same random sequence when
started from the same seed, even across different environments and versions of
Panda.
================================================================================
class XorShiftRandom (Random) {
    ============================================================================
    The current seed.
    ============================================================================
    @private 
    var _seed:UInt64
    
    ============================================================================
    Creates a new, automatically-seeded `XorShiftRandom`.
    ============================================================================
    constructor() {
        constructor(Random.int64())
    }
    
    ============================================================================
    Creates a new `XorShiftRandom` with a specific seed. All `XorShiftRandom` 
    instances created with the same seed will return the same sequence of 
    pseudorandom numbers.
    
    @param seed the seed to use
    ============================================================================
    constructor(seed:Int64) {
        self._seed := seed->>(UInt64)
    }
    
    @self
    method seed:=(seed:Int64) {
        if seed = 0 {
            -- XorShift seed cannot be zero, replace with an arbitrarily-chosen
            -- value
            _seed := 12345678
        }
        else
            _seed := seed->>(UInt64)
    }

    @override
    @math(overflow)
    method int64():Int64 {
         _seed := _seed ~~ (_seed >> 12)
        _seed := _seed ~~ (_seed << 25)
        _seed := _seed ~~ (_seed >> 27)
        return _seed->>(Int64) * 2685821657736338717
    }
}