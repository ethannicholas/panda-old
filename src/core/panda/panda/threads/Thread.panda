package panda.threads

================================================================================
Represents a thread of execution within a program. Each program begins with a
single thread, and may create more. By default, any living threads prevent a 
program from exiting, even if its `main()` method ends. The [preventsExit] flag
may be toggled to change this behavior.

As Panda has neither global nor class-level variables, threads do not (usually)
have any access to each other's data. Eliminating memory sharing means that 
there is no need for mutexes, semaphores, or other common thread synchronization 
constructs; mutable objects are only accessible from a single thread and there
is therefore no contention. 

Threads communicate with each other using [MessageQueue] objects, which normally
only permit [Immutable] objects to be sent. Mutable objects may be sent, but at
the cost of terminating the calling thread. This ensures that only one thread
can access a given mutable object at a time.

In situations where it really is necessary to have multiple threads access
potentially-shared mutable objects, the [postUnsafe](MessageQueue::postUnsafe)
method exists. This allows you to pass mutable objects between threads without
terminating the calling thread. If the object (including any other mutable 
objects it directly or indirectly points to) is truly 'handed off' and no longer 
accessed by the sending thread, no danger exists. If the sending thread 
continues to access the sent object, [Lock] must be used to synchronize access 
to the shared object.
================================================================================
@final
class Thread : Immutable {
    @private
    def nativeThread:$NativePointer

    ============================================================================
    A per-thread MessageQueue which may be used for any purpose.
    ============================================================================
    def queue := new MessageQueue()

    ============================================================================
    `true` if this thread will prevent the program from exiting. The program 
    exits when there are zero `preventsExit` threads remaining.
    ============================================================================
    def preventsExit:Bit

    @private
    def threadLocals:$NativePointer
    
    ============================================================================
    A collection of immutable objects which may be accessed from anywhere in the
    current thread, and which may be inherited by newly-created child threads. 
    The default thread context is empty.
    ============================================================================
    @thread
    def context:GenericMap<Immutable, Immutable> := new GenericHashMap<Immutable, Immutable>()

    @private
    def threadEntry:()=&>*()

    @private
    constructor() {
        -- dummy constructor for Java to easily create the main thread
        threadEntry := method() {
            Console.writeLine("You shouldn't see this...")
        }
    }

    @private
    constructor(threadEntry:()=&>*(), preventsExit:Bit) {
        self.threadEntry := threadEntry
        self.preventsExit := preventsExit
    }

    ============================================================================
    The thread entry method that is called when the thread starts up. This 
    method is called automatically by [start()]; you should not call it 
    manually.

    The default implementation does nothing.

    @see start()
    ============================================================================
    @private
    method run() {
        threadEntry()
    }

    ============================================================================
    Starts the thread, calling its [run()] method. The new thread's context will 
    contain all objects from the current thread's context.

    @see run()
    ============================================================================
    @final
    @private
    method start() {
        startThread(new GenericHashMap<Immutable, Immutable>(context))
    }

    ============================================================================
    Starts the thread with a new context, calling its [run()] method. The new
    thread's context (available from the [context] field) will be a union of the
    current thread's context and the specified `newContext` map.
    
    @param newContext the new thread's context
    ============================================================================
    @final
    @private
    method start(newContext:GenericMapView<Immutable, Immutable>) {
        var merged := new GenericHashMap<Immutable, Immutable>()
        merged.addAll(context)
        merged.addAll(newContext)
        startThread(merged)
    }
    
    ============================================================================
    Returns the `Thread` that is currently executing.
    
    @returns the current `Thread`
    ============================================================================
    @class
    @external
    function currentThread():Thread

    ============================================================================
    Creates and starts a new thread which executes the specified method. For
    example, to create a new thread which continually displays a message to the 
    console:

        Thread.start(method() {
            loop {
                Console.writeLine("Hi, I'm a thread!")
            }
        })

    @param run the run method to execute
    @returns the newly created thread
    ============================================================================
    @class
    method start(run:()=&>*()):Thread {
        var result := new Thread(run, true)
        result.start()
        return result
    }

    @class
    method start(run:()=&>*(), context:GenericMapView<Immutable, Immutable>):Thread {
        var result := new Thread(run, true)
        result.start(context)
        return result
    }

    @class
    method start(run:()=&>*(), context:GenericMapView<Immutable, Immutable>, preventsExit:Bit):Thread {
        var result := new Thread(run, preventsExit)
        result.start(context)
        return result
    }

    @class
    method start(run:()=&>*(), preventsExit:Bit):Thread {
        var result := new Thread(run, preventsExit)
        result.start()
        return result
    }

    ============================================================================
    Returns the number of threads that generally leads to best performance on
    the current system. This will generally be (but is not necessarily) the
    number of available processor cores.

    **IMPLEMENTATION NOTE:** this is not currently implemented and is just 
    hardcoded to return 4.

    @returns the preferred number of executing threads
    ============================================================================
    @class
    function preferredThreadCount():Int {
        return 4
    }
    
    ============================================================================
    Pauses execution of the current thread for a length of time.
    
    @param millis the number of milliseconds to sleep
    ============================================================================
    @class 
    @external
    method sleep(millis:Int)
        
    @private
    @external
    method startThread(context:GenericHashMap<Immutable, Immutable>)

    @private
    @class
    @external
    method threadExit()
}