package panda.io

================================================================================
A `FilterInputStream` which tracks the current line and column as it reads from
underlying stream.
================================================================================
class LineNumberInputStream : FilterInputStream {
    ============================================================================
    The current line number (starting at 1).
    ============================================================================
    var line := 1

    ============================================================================
    The current column number (starting at 1).
    ============================================================================
    var column := 1

    ============================================================================
    Creates a new `LineNumberInputStream`.

    @param raw the underlying stream
    ============================================================================
    constructor(raw:InputStream) {
        super.constructor(raw)
    }

    @override
    method readInt8():Int8? {
        var result := super.readInt8()
        if result = "\n"->(Int8) {
            line += 1
            column := 1
        }
        else
            column += 1
        return result
    }
    
    @override
    method read(bytes:Array<Int8>):Int? {
        var result := super.read(bytes)
        if result != null {
            for i in 0 ... result - 1 {
                if bytes[i] = "\n"->(Int8) {
                    line += 1
                    column := 1
                }
                else
                    column += 1
            }
        }
        return result
    }
}