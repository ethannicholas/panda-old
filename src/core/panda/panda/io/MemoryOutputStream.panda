package panda.io

================================================================================
An `OutputStream` which stores data written to it in memory. After writing to
it, its contents may be converted to an `PrimitiveArray` or to a `String`.
================================================================================
class MemoryOutputStream : OutputStream {
    property length:Int

    @private
    constant DEFAULT_LENGTH := 16
    
    ============================================================================
    The data buffer
    ============================================================================
    @private 
    def buffer:Array<UInt8>
    
    ============================================================================
    Creates a new `MemoryOutputStream`.
    ============================================================================
    init() {
        init(DEFAULT_LENGTH)
    }
    
    ============================================================================
    Creates a new `MemoryOutputStream` with the specified initial capacity (in
    bytes).
    
    @param size the size of the memory buffer
    ============================================================================
    init(size:Int32) {
        buffer := Array<UInt8>()
    }

    @private
    function get_length():Int {
        return buffer.length
    }
    
    @self
    @override
    method write(b:UInt8) {
        buffer.add(b)
    }
    
    @self
    @override
    method write(bytes:ListView<UInt8>, offset:Int, length:Int) {
        buffer.add(bytes, offset, length)
    }

    ============================================================================
    Returns a single `Int8` from the buffer.
    
    @param i the index of the `Int8` to return
    @returns the `Int8` at that index
    ============================================================================
    @pre(i < length)
    function [](i:Int):UInt8 {
        return buffer[i]
    }
    
    ============================================================================
    Returns a copy of the memory buffer as a `Int8` array.
    
    @returns all data written to the stream
    ============================================================================
    function convert():Array<UInt8> {
        def result := Array<UInt8>()
        for b in buffer
            result.add(b)
        return result
    }

    ============================================================================
    Returns a copy of the memory buffer as a `Char` array.
    
    @returns all data written to the stream
    ============================================================================
    function convert():Array<Char> {
        -- FIXME obviously missing encodings here...
        def result := Array<Char>()
        for b in buffer
            result.add(b.convert()->Char)
        return result

    }

    ============================================================================
    Returns a copy of the memory buffer as a String.
    
    @param fmt the format string, must be `""`
    @returns all data written to the stream
    ============================================================================
    @override
    function convert():String {
        return String(self.convert()->Array<Char>)
    }

    ============================================================================
    Sends the contents of the buffer to an output stream.
    ============================================================================
    method sendTo(o:OutputStream) {
        o.write(buffer)
    }

    ============================================================================
    Discards all data written to the stream so far.
    ============================================================================
    method reset() {
        buffer.clear()
    }
    @post(length = 0)
}