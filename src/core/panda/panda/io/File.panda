package panda.io

================================================================================
Represents a file or directory in the filesystem. Provides methods to read, 
write, and otherwise manipulate the file.
================================================================================
@final
class File : Immutable {
    ============================================================================
    The path separator character on the current platform. Since Panda doesn't
    support Windows yet, this is just hardcoded to `"/"` for the time being.
    ============================================================================
    constant SEPARATOR := "/"

    ============================================================================
    File representing the system temporary directory.
    ============================================================================
    -- FIXME the hardcoded value is, ironically, temporary
    constant TEMP               := File("/tmp")

    ============================================================================
    The path to the file.
    ============================================================================
    def path:String
    
    ============================================================================
    Creates a `File` referencing the specified path. Creating a `File` object
    does not, by itself, create a physical file on disk; the `File` object
    merely stores the path and allows filesystem operations to take place
    against this path.
    
    @param path the (relative or absolute) path to the file
    ============================================================================
    init(path:String) {
        self.path := path
    }
        
    ============================================================================
    Returns a `File` representing the current directory when the program was
    started. This directory cannot be changed while the program is running, as
    it would cause any relative paths to suddenly stop working.

    @returns the program's working directory
    ============================================================================
    @class
    method currentDirectory():File {
        return File(".")
    }
    
    ============================================================================
    Returns the name of the file, without its path information.
    
    @returns the name of the file
    ============================================================================
    property name:String

    @private
    function get_name():String {
        def index := path.lastIndexOf(SEPARATOR)
        if index = null
            return path
        else
            return path[index + 1..]
    }
    
    ============================================================================
    Returns the simple name of the file, without its extension. For instance,
    new `File("/tmp/foo.tar.gz").simpleName` returns `"foo.tar"`.
    
    @returns the simple name of the file
    ============================================================================
    property simpleName:String

    @private
    function get_simpleName():String {
        def index := name.lastIndexOf(".")
        if index != null
            return name[0 .. index]
        else
            return name
    }

    ============================================================================
    Returns the file's extension, including the leading dot. For instance,
    new `File("/tmp/foo.tar.gz").simpleName` returns `".tar.gz"`. If the 
    filename does not contain a dot, the empty string is returned.
    
    @returns the file's extension
    ============================================================================
    property extension:String

    @private
    function get_extension():String {
        def index := name.indexOf(".")
        if index != null
            return name[index..]
        else
            return ""
    }
    
    ============================================================================
    Returns a `File` representing the absolute path of this file.
    
    @returns the file's absolute path
    ============================================================================
    method absolute():File {
        return File(absolutePath())
    }
    
    ============================================================================
    Returns the directory containing this file, or `null` if the file does not
    have a parent.
    
    @returns the path to the file
    ============================================================================
    method parent():File? {
        if path = "/"
            return null
        def index := path.lastIndexOf(SEPARATOR)
        if index != null
            return File(path[0 .. index])
        else
            return absolute().parent()
    }
    
    ============================================================================
    Resolves a potentially-relative path in the context of this `File`. For 
    instance, if `foo` is the path `"/tmp"`, `foo.resolve("bar")` is equivalent 
    to the path `"/tmp/bar"`. When passed an absolute path, such as 
    `foo.resolve("/bar")`, the result is the absolute path (in this case 
    `"/bar"`).
    
    @param path the path to resolve
    @returns a file referring to the relative path
    ============================================================================
    function resolve(path:String):File {
        if path.startsWith("/")
            return File(path)
        return File(self.path + SEPARATOR + path)
    }
    
    ============================================================================
    Returns `true` if this file exists.
    
    @returns `true` if this file exists
    ============================================================================
    @external
    method exists():Bit
        
    ============================================================================
    Returns `true` if this file represents a directory.
    
    @returns `true` if this is a directory
    ============================================================================
    @external
    method isDirectory():Bit
        
    ============================================================================
    Creates a directory at this path. It is not an error to attempt to create a
    directory which already exists.
    ============================================================================
    @external
    method createDirectory()
    
    ============================================================================
    Creates a directory at this path, including all required parent directories.
    It is not an error to attempt to create a directory which already exists.
    ============================================================================
    method createDirectories() {
        def p := parent()
        if p != null & !p.exists()
            p.createDirectories()
        createDirectory()
    }
        
    ============================================================================
    Returns a list of files contained in this directory.
    
    @returns the contents of this directory
    ============================================================================
    @external
    method list():ListView<File>
    
    @override
    function convert():String {
        return path
    }

    ============================================================================
    Deletes the file. If the file is a directory, it must be empty or an 
    exception will be thrown. FIXME fix spec once exceptions are in place.
    ============================================================================
    @external
    method delete()
    
    ============================================================================
    Returns a list of files in this directory matching a wildcard pattern.
    Asterisks ("*") in the pattern mean "match any number of characters", and
    question marks ("?") in the pattern mean "match a single character".
    For example, `directory.list("*.png")` will return a list of all files with
    the extension `".png"`.
    
    @param pattern the wildcard pattern to match
    @returns the files matching the pattern
    ============================================================================
    method list(pattern:String):ListView<File> {
        def result := MutableString()
        for c in pattern {
            if "+?|{[()^$.#\\".contains(c)
                result.append("\\")
            if c = "*"
                result.append(".")
            if c = "?"
                result.append(".")
            else
                result.append(c)
        }
        return list(RegularExpression(result.convert()))
    }

    ============================================================================
    Returns a list of files in this directory matching a regular expression.
    For example, `directory.list(/[abc].*\.png/)` will return a list of all 
    files with the extension `".png"` that begin with the characters `a`, `b`,
    or `c`.
    
    @param pattern the wildcard pattern to match
    @returns the files matching the pattern
    ============================================================================
    method list(pattern:RegularExpression):ListView<File> {
        return list().filter(f => f.name.matches(pattern))->ListView<File>
    }

    ============================================================================
    Returns a new `InputStream` which reads from the file.
    
    @returns a stream for reading the file
    ============================================================================
    method openInputStream():InputStream {
        return FileInputStream(self)
    }
        
    ============================================================================
    Returns a new `OutputStream` for writing to the file. The file is reset to
    zero length as part of opening the output stream.
    
    @returns a stream for reading the file
    ============================================================================
    method openOutputStream():OutputStream {
        return FileOutputStream(self)
    }
    
    ============================================================================
    Replaces the contents of the file with the specified string.
    
    @param s the string to write
    ============================================================================
    method write(s:String) {
        def out := openOutputStream()
        out.write(s)
        out.close()
    }
    
    ============================================================================
    Replaces the contents of the file with the specified data.
    
    @param bytes the data to write
    ============================================================================
    method write(bytes:ListView<Int8>) {
        def out := openOutputStream()
        out.write(bytes)
        out.close()
    }

    ============================================================================
    Reads the entire file into a `String` in memory.

    @returns the contents of the file
    ============================================================================
    method readAsString():String {
        def buffer := MemoryOutputStream()
        openInputStream().sendTo(buffer)
        return buffer.convert()
    }

    ============================================================================
    Reads the entire file into an `Array<Int8>` in memory.

    @returns the contents of the file
    ============================================================================
    method readAsInt8Array():Array<Int8> {
        def buffer := MemoryOutputStream()
        openInputStream().sendTo(buffer)
        return buffer.convert()->Array<Int8>
    }

    ============================================================================
    Reads the entire file into an `Array<UInt8>` in memory.

    @returns the contents of the file
    ============================================================================
    method readAsUInt8Array():Array<UInt8> {
        def buffer := MemoryOutputStream()
        openInputStream().sendTo(buffer)
        return buffer.convert()->Array<UInt8>
    }

    ============================================================================
    Returns the contents of the file broken up into lines.

    @returns the lines of the file
    ============================================================================
    method lines():Iterator<String> {
        -- FIXME shouldn't have to read the whole file ahead of time
        def result := readAsString().split(/\r?\n/)
        Console.writeLine("read lines: " + result)
        return result.iterator()
    }
    
    @override
    function =(o:Object):Bit {
        if o-!>File
            return false
        return path = o->File.path
    }

    @override
    function get_hash():Int {
        return path.hash
    }
        
    @private
    @external
    method absolutePath():String
}