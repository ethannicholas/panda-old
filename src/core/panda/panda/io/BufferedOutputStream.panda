package panda.io

================================================================================
A `FilterOutputStream` which buffers data written to its underlying stream, 
writing it in chunks. This causes fewer (and bigger) writes to the underlying
stream, generally making writes to files and network sockets much faster.

Data written to a `BufferedOutputStream` can be held in memory for an arbitrary 
length of time without being written to the underlying stream; it is important 
to call `flush()` or `close()` to ensure that data is written in a timely 
fashion.
================================================================================
class BufferedOutputStream : FilterOutputStream {
    @private
    constant DEFAULT_BUFFER_SIZE := 8192    

    @private
    var bufferOffset := 0

    @private
    def buffer:Array<UInt8>

    @private
    def bufferSize:Int

    ============================================================================
    Creates a new `BufferedOutputStream`.

    @param out the underlying output stream
    ============================================================================
    init(out:OutputStream) {
        init(out, DEFAULT_BUFFER_SIZE)
    }

    ============================================================================
    Creates a new `BufferedOutputStream` with a specific buffer size.

    @param out the underlying output stream
    @param bufferSize the size of the buffer in bytes
    ============================================================================
    init(out:OutputStream, bufferSize:Int) {
        super.init(out)
        self.bufferSize := bufferSize
        self.buffer := Array<UInt8>(bufferSize, 0)
    }

    @override
    method write(i:UInt8) {
        if bufferOffset = bufferSize
            flush()
        buffer[bufferOffset] := i
        bufferOffset += 1
    }

    @override
    method write(b:ListView<UInt8>, var offset:Int, var length:Int) {
        while bufferOffset + length > bufferSize {
            def bytes := bufferSize - bufferOffset
            write(b, offset, bytes)
            offset += bytes
            length -= bytes
            flush()
        }
        for i in offset .. offset + length {
            buffer[bufferOffset] := b[i]
            bufferOffset += 1
        }
    }

    @override
    method flush() {
        super.write(buffer, 0, bufferOffset)
        bufferOffset := 0
    }

    @override
    method close() {
        flush()
        super.close()
    }
}