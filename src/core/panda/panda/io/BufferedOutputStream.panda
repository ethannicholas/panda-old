package panda.io

================================================================================
A `FilterOutputStream` which buffers data written to its underlying stream, 
writing it in chunks. This causes fewer (and bigger) writes to the underlying
stream, generally making writes to files and network sockets much faster.

Data written to a `BufferedOutputStream` can be held in memory for an arbitrary 
length of time without being written to the underlying stream; it is important 
to call `flush()` or `close()` to ensure that data is written in a timely 
fashion.
================================================================================
class BufferedOutputStream : FilterOutputStream {
    @private
    constant DEFAULT_BUFFER_SIZE := 8192    

    @private
    def buffer := new Array<Int8>()

    @private
    def bufferSize:Int

    ============================================================================
    Creates a new `BufferedOutputStream`.

    @param out the underlying output stream
    ============================================================================
    constructor(out:OutputStream) {
        constructor(out, DEFAULT_BUFFER_SIZE)
    }

    ============================================================================
    Creates a new `BufferedOutputStream` with a specific buffer size.

    @param out the underlying output stream
    @param bufferSize the size of the buffer in bytes
    ============================================================================
    constructor(out:OutputStream, bufferSize:Int) {
        super.constructor(out)
        self.bufferSize := bufferSize
    }

    @override
    method write(i:Int8) {
        if buffer.length = bufferSize
            flush()
        buffer.add(i)
    }
    
    @override
    method write(b:ListView<Int8>, var offset:Int, var length:Int) {
        while buffer.length + length > bufferSize {
            def bytes := bufferSize - buffer.length
            write(b, offset, bytes)
            offset += bytes
            length -= bytes
            flush()
        }
        for i in offset .. offset + length
            buffer.add(b[i])
    }

    @override
    method write(b:ListView<UInt8>, var offset:Int, var length:Int) {
        while buffer.length + length > bufferSize {
            def bytes := bufferSize - buffer.length
            write(b, offset, bytes)
            offset += bytes
            length -= bytes
            flush()
        }
        for i in offset .. offset + length
            buffer.add(b[i]->>(Int8))
    }

    @override
    method write(c:ListView<Char>, var offset:Int, var length:Int) {
        while buffer.length + length > bufferSize {
            def bytes := bufferSize - buffer.length
            write(c, offset, bytes)
            offset += bytes
            length -= bytes
            flush()
        }
        for i in offset .. offset + length
            buffer.add(c[i]->>(Int8))
    }

    @override
    method flush() {
        super.write(buffer, 0, buffer.length)
        buffer.clear()
    }

    @override
    method close() {
        flush()
        super.close()
    }
}