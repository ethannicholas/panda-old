package panda.collections

class Array<T> (List<T>) {
    @private
    constant DEFAULT_LENGTH := 16

    @private
    var contents:PrimitiveArray<T>

    @private
    var _length := 0

    constructor() {
        contents := new PrimitiveArray<T>(DEFAULT_LENGTH)
    }

    constructor(length:Int) {
        contents := new PrimitiveArray<T>(length)
    }

    constructor(length:Int, fill:T) {
        contents := new PrimitiveArray<T>(length)
        _length := length
        for i in 0 .. length
            contents[i] := fill
    }

    ============================================================================
    @hidden
    ============================================================================
    constructor(contents:PrimitiveArray<T>) {
        self.contents := contents
        self._length := contents.length
    }

    constructor(contents:CollectionView<T>) {
        self.contents := new PrimitiveArray<T>(contents.length)
        for (i, v) in contents.enumeration()
            self.contents[i] := v
        _length := contents.length
    }

    @override
    @preOr(index < _length)
    function [](index:Int):T {
        return contents[index]
    }

    @override
    function length():Int {
        return _length
    }

    @self
    @override
    @preOr(index < _length)
    method []:=(index:Int, value:T) {
        contents[index] := value
    }

    @override
    @self
    method add(value:T) {
        ensureCapacity(_length + 1)
        _length += 1
        self[_length - 1] := value
    }

    @override
    @self
    method remove(object:T) {
        for i in 0 .. length {
            if self[i] = object {
                removeIndex(i)
                break
            }
        }
    }

    @override
    @self
    method removeIndex(index:Int) {
        for i in index .. length - 1
            self[i] := self[i + 1]
        _length -= 1
    }

    @override
    @self
    method clear() {
        contents := new PrimitiveArray<T>(DEFAULT_LENGTH)
        _length := 0
    }

    @override
    function contains(object:T):Bit {
        for v in self {
            if object = v
                return true
        }
        return false
    }

    @private
    @self
    method ensureCapacity(maxLength:Int) {
        if maxLength > contents.length {
            def newSize := (contents.length * 2).max(maxLength)
            def newContents := new PrimitiveArray<T>(newSize)
            for i in 0 .. length
                newContents[i] := contents[i]
            contents := newContents
        }
    }

    @override
    function =(o:Object):Bit {
        if o-!>ListView<T>
            return false
        def list := o->ListView<T>
        if length != list.length
            return false
        for (i, v) in enumeration() {
            if v != list[i]
                return false
        }
        return true
    }

    @override
    @math(overflow)
    function hash():Int {
        var result := 0
        constant MULTIPLIER := 53
        for v in self {
            result *= MULTIPLIER
            if v != null
                result += v.hash
        }
        return result
    }

    @override
    function ->>():String {
        def result := new MutableString("[")
        for (i, v) in enumeration() {
            if i > 0
                result.append(", ")
            result.append(v)
        }
        result.append("]")
        return result->>String
    }
}