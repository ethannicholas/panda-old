package panda.collections

================================================================================
Implementation of the `Stack` abstract data type.
================================================================================
@specialize
class Stack<T> {
    @private
    def contents:List<T> := Array<T>()

    ============================================================================
    Creates a new, empty `Stack`.
    ============================================================================
    init() {
    }
    @post(count = 0)
    
    ============================================================================
    Pushes a new value onto the stack.

    @param o the object to push
    @see pop
    ============================================================================
    method push(value:T) {
        contents.add(value)
    }
    @post(count = @pre(count) + 1)
    
    ============================================================================
    Pops the top element of the stack and returns it.

    @returns the popped element
    @see push
    ============================================================================
    @pre(count > 0)
    method pop():T {
        def result := contents[contents.count - 1]
        contents.removeIndex(contents.count - 1)
        return result
    }
    @post(count = @pre(count) - 1)

    ============================================================================
    Removes all elements from the stack.
    ============================================================================
    method clear() {
        contents.clear()
    }
    
    ============================================================================
    Returns the top element of the stack without removing it.

    @returns the top element of the stack
    @see peek(Int)
    ============================================================================
    function peek():T {
        return peek(0)
    }

    ============================================================================
    Returns an arbitrary element of the stack without removing it. The top of
    the stack is at depth 0 and the bottommost element of the stack is at depth
    `count - 1`.
    
    @param depth the index of the element to return
    @returns the specified stack element
    @see peek()
    ============================================================================
    @pre(depth < count)
    @pre(depth >= 0)
    function peek(depth:Int):T {
        return contents[(count - depth) - 1]
    }
    
    ============================================================================
    Returns the number of elements in the stack.
    ============================================================================
    property count:Int

    @private
    function get_count():Int {
        return contents.count
    }

    ============================================================================
    Returns the contents of the stack formatted as a string, with the top of the
    stack to the right.

    @returns the stack formatted as a string
    ============================================================================
    @override
    function convert():String {
        return contents.convert()
    }

    ============================================================================
    Converts the stack to a ListView. The bottom of the stack is at index `0` 
    and the top of the stack is at the end of the array.

    @returns the contents of the stack
    ============================================================================
    function convert():ListView<T> {
        return contents[..]
    }
}