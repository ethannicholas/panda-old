package panda.collections

interface List<T> : ListView<T>, ListWriter<T> {
    -- FIXME need to make this generic over <V:T> and change the signature to
    -- (T)=>(V) when generic methods are in
    @self
    method mapInPlace(f:(T)=>(T)) {
        for i in 0 .. length
            self[i] := f(self[i])
    }

    @self
    method filterInPlace(f:(T)=>(Bit)) {
        for i in length - 1 ... 0 by -1 {
            if !f(self[i])
                removeIndex(i)
        }
    }

    @override
    method insert(value:Int, index:Int) {
        add(value)
        for i in length - 1 .. index by -1
            self[i] := self[i - 1]
        self[index] := value
    }

    method []:=(i:Int, object:T)

    @self
    method [..]:=(var start:Int?, var end:Int?, step:Int, list:ListView<T>) {
        if start = null {
            if step > 0
                start := 0
            else
                start := length - 1
        }
        if end = null {
            if step > 0
                end := length
            else
                end := -1
        }
        self[start->(Int) .. end->(Int) by step] := list
    }

    @self
    method [..]:=(start:Int, end:Int, step:Int, list:ListView<T>) {
        if step = 1 {
            def oldLength := end - start
            def newLength := list.length 
            def delta := newLength - oldLength
            if delta < 0 {
                -- list is getting shorter
                -- This is not the fastest way to do this, as we end up shifting
                -- the same elements multiple times, but it's simpler and easier
                -- than the smart way of doing it.
                -- FIXME PERFORMANCE
                for i in 0 .. -delta
                    removeIndex(start)
                for i in start .. start + list.length
                    self[i] := list[i - start]
            }
            else if delta > 0 {
                -- list is getting longer
                -- insert the first /delta/ elements to increase the list length
                -- by the desired amount. This is not the fastest way to do 
                -- this, as we end up shifting the same elements multiple times,
                -- but it's simpler and easier than the smart way of doing it.
                -- FIXME PERFORMANCE
                for i in 0 .. delta
                    insert(list[i], start + i)
                -- the rest of the list overwrites existing elements
                for i in delta .. list.length
                    self[start + i] := list[i]
            }
        }
        else {
            var src := 0
            for dest in start .. end by step {
                self[dest] := list[src]
                src += 1
            }
            assert src = list.length : new PreconditionError(
                    "slice source and destination lengths do not match")
        }
    }

    @self
    method [...]:=(var start:Int?, var end:Int?, step:Int, list:ListView<T>) {
        if start = null {
            if step > 0
                start := 0
            else
                start := length - 1
        }
        if end = null {
            if step > 0
                end := length - 1
            else
                end := 0
        }
        self[start->(Int) ... end->(Int) by step] := list
    }

    @self
    method [...]:=(start:Int, end:Int, step:Int, list:ListView<T>) {
        if step > 0
            self[start .. end + 1 by step] := list
        else
            self[start .. end - 1 by step] := list
    }
}
