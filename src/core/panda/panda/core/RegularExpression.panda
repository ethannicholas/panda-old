package panda.core

================================================================================
A regular expression which can be used to match strings and extract text from
them.

`RegularExpression` supports Perl 5-compatible regular expression syntax. The 
syntax `#/expression/#` may be used as a shortcut to embed `RegularExpression` 
objects within Panda source code.
================================================================================
@static
class RegularExpression : Immutable {
    ============================================================================
    Flag controlling the behavior of the `$` and `^` tokens. When a 
    `RegularExpresison` is created with the `MULTILINE` flag, `$` and `^` match
    only the beginning / end of the entire string rather than the beginning /
    end of a single line.
    ============================================================================
    constant MULTILINE := 1

    ============================================================================
    The pattern text.
    ============================================================================
    def pattern:String
    
    ============================================================================
    Pointer to the native `URegularExpression`.

    @hidden
    ============================================================================
    --@package FIXME uncomment this
    def nativeHandle:$NativePointer
    
    ============================================================================
    Creates a new `RegularExpression` compiled from the specified regular
    expression string.
    
    @param pattern the expression to compile
    ============================================================================
    constructor(pattern:String) {
        constructor(pattern, 0)
    }

    ============================================================================
    Creates a new `RegularExpression` compiled from the specified regular
    expression string, with flags controlling the regular expression's behavior.
    
    @param pattern the expression to compile
    @param flags bitmask of flag bits
    ============================================================================
    constructor(pattern:String, flags:Int) {
        self.pattern := pattern
        compile(pattern, flags)
    }
    
    ============================================================================
    Returns a matcher which can be used to scan the string for occurrences of
    the regular expression.

    @param text the string to scan
    @returns a matcher which can be used to scan the string
    ============================================================================
    @safeReturn
    function matcher(text:String):Matcher {
        return new Matcher(self, text)
    }

    @override
    function format(fmt:String):String {
        return "#/" + pattern + "/#"
    }
    
    -- release the native resources
    @override
    method finalize() {
        close()
    }
    
    ============================================================================
    Escapes characters within a string so that they will be interpreted 
    literally rather than as special characters (`*`, `+`, `?`, `|`, `{`, `[`, 
    `(`, `)`, `^`, `$`, `.`, `#`, and `\`).
    
    @param s the string to escape
    @returns a copy of `s` with special characters escaped
    ============================================================================
    @class
    method escape(s:String):String {
        var result := new MutableString()
        for i in 0 ... s.length - 1 {
            var c := s[i]
            if "*+?|{[()^$.#\\".contains(c)
                result.append("\\")
            result.append(c)
        }
        return result->>(String)
    }

    ============================================================================
    @hidden
    ============================================================================
    --@package FIXME uncomment this
    @external
    function clone():RegularExpression

    @private
    @external
    method compile(regex:String, flags:Int)

    ============================================================================
    @hidden
    ============================================================================
    @external
    method close()
}