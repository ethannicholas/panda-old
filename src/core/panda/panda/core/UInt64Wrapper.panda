package panda.core

================================================================================
A 64 bit unsigned integer, capable of representing numbers in the range 0 to 
18,446,744,073,709,551,615.

`UInt64`s are created automatically as the result of integer literals (numbers 
appearing directly in the source code) and as the result of mathematical 
expressions.
================================================================================
@static
class UInt64Wrapper : UnsignedInteger {
    ============================================================================
    The minimum value representable by a `UInt64`.
    ============================================================================
    constant MIN:UInt64 := 0
    
    ============================================================================
    The maximum value representable by a `UInt64`.
    ============================================================================
    constant MAX:UInt64 := 18446744073709551615

    ============================================================================
    The number of bits in a `UInt64`.
    ============================================================================
    constant BITS := 64

    ============================================================================
    @hidden
    ============================================================================
    def value:UInt64
    
    ============================================================================
    Creates a slice of the integers: an array ranging from `left` (inclusive) to
    `right` (exclusive).
    ============================================================================
    @class
    function [..](left:UInt64, right:UInt64):Array<UInt64> {
        def result := new Array<UInt64>()
        for v:UInt64 in left .. right
            result.append(v)
        return result
    }

    ============================================================================
    Creates a slice of the integers: an array ranging from `left` (inclusive) to
    `right` (exclusive), counting by `step`.
    ============================================================================
    @class
    @math(overflow)
    function [..](left:UInt64, right:UInt64, step:Int64):Array<UInt64> {
        def result := new Array<UInt64>()
        var i := left
        def stepAbs := step.abs->(UInt64)
        if step > 0 {
            if i < right {
                loop {
                    result.append(i)
                    if right - i <= stepAbs
                        break
                    i += stepAbs
                }
            }
        }
        else {
            if i > right {
                loop {
                    result.append(i)
                    if i - right <= stepAbs
                        break
                    i -= stepAbs
                }
            }
        }
        return result
    }

    ============================================================================
    Creates a slice of the integers: an array ranging from `left` (inclusive) to
    `right` (exclusive), counting by `step`.
    ============================================================================
    @class
    @math(overflow)
    function [..](left:UInt64, right:UInt64, step:UInt64):Array<UInt64> {
        def result := new Array<UInt64>()
        for v:UInt64 in left .. right by step
            result.append(v)
        return result
    }

    @class 
    function [...](left:UInt64):Array<UInt64> {
        throw new UnimplementedException()
    }
    
    ============================================================================
    Creates a slice of the integers: an array ranging from `left` to `right` 
    (inclusive).
    ============================================================================
    @class
    function [...](left:UInt64, right:UInt64):Array<UInt64> {
        var result := new Array<UInt64>()
        for v:UInt64 in left ... right
            result.append(v)
        return result
    }

    ============================================================================
    Creates a slice of the integers: an array ranging from `left` to `right` 
    (inclusive), counting by `step`.
    ============================================================================
    @class
    @math(overflow)
    function [...](left:UInt64, right:UInt64, step:Int64):Array<UInt64> {
        def result := new Array<UInt64>()
        var i := left
        def stepAbs := step.abs->(UInt64)
        if step > 0 {
            if i <= right {
                loop {
                    result.append(i)
                    if right - i < stepAbs
                        break
                    i += stepAbs
                }
            }
        }
        else {
            if i >= right {
                loop {
                    result.append(i)
                    if i - right < stepAbs
                        break
                    i -= stepAbs
                }
            }
        }
        return result
    }

    ============================================================================
    Creates a slice of the integers: an array ranging from `left` to `right` 
    (inclusive), counting by `step`.
    ============================================================================
    @class
    @math(overflow)
    function [...](left:UInt64, right:UInt64, step:UInt64):Array<UInt64> {
        def result := new Array<UInt64>()
        for v:UInt64 in left ... right by step
            result.append(v->(UInt64))
        return result
    }

    ============================================================================
    @hidden
    ============================================================================
    constructor(value:UInt64) {
        self.value := value
    }
        
    -- The operators below are *not actually used* by naive code; i.e. x * y
    -- will never call UInt64::* (which is why none of these end up causing
    -- infinite recursion). They are specifically filtered out by 
    -- ASTGenerator::callOverloadedOperator, and defined here purely for
    -- convenience so that they can be accessed like "UInt64::*".

    @class
    function +(x:UInt64, y:UInt64):UInt64 {
        return x + y
    }

    @class
    function -(x:UInt64, y:UInt64):UInt64 {
        return x - y
    }

    @class
    function *(x:UInt64, y:UInt64):UInt64 {
        return x * y
    }

    @class
    function /(x:UInt64, y:UInt64):Real64 {
        return x / y
    }

    @class
    function //(x:UInt64, y:UInt64):UInt64 {
        return x // y
    }

    @class
    function ^(x:UInt64, y:UInt32):UInt64 {
        return x ^ y
    }

    @class
    function ^(x:UInt64, y:Real32):Real64 {
        return x ^ y
    }

    @class
    function ||(x:UInt64, y:UInt64):UInt64 {
        return x || y
    }

    @class
    function &&(x:UInt64, y:UInt64):UInt64 {
        return x && y
    }

    @class
    function ~~(x:UInt64, y:UInt64):UInt64 {
        return x ~~ y
    }

    @class
    function !!(x:UInt64):UInt64 {
        return !!x
    }

    @class
    function >(x:UInt64, y:UInt64):Bit {
        return x > y
    }

    @class
    function >=(x:UInt64, y:UInt64):Bit {
        return x >= y
    }

    @class
    function <(x:UInt64, y:UInt64):Bit {
        return x < y
    }

    @class
    function <=(x:UInt64, y:UInt64):Bit {
        return x <= y
    }

    ============================================================================
    Returns the greater (closer to positive infinity) of itself and its 
    parameter.

    @param other the number to compare against
    @returns the maximum of itself and the other number
    ============================================================================
    function max(other:UInt64):UInt64 {
        if value > other
            return value
        else
            return other
    }

    ============================================================================
    Returns the greater (closer to positive infinity) of itself and its 
    parameter.

    @param other the number to compare against
    @returns the maximum of itself and the other number
    ============================================================================
    function max(other:Real64):Real64 {
        if value > other
            return value
        else
            return other
    }

    ============================================================================
    Returns the lesser (closer to negative infinity) of itself and its 
    parameter.

    @param other the number to compare against
    @returns the minimum of itself and the other number
    ============================================================================
    function min(other:UInt64):UInt64 {
        if value < other
            return value
        else
            return other
    }


    ============================================================================
    Returns the lesser (closer to negative infinity) of itself and its 
    parameter.

    @param other the number to compare against
    @returns the minimum of itself and the other number
    ============================================================================
    function min(other:Real64):Real64 {
        if value < other
            return value
        else
            return other
    }

    function sign():Int {
        if value > 0
            return 1
        else if value < 0
            return -1
        return 0
    }

    function abs():UInt64 {
        if value < 0
            return -value
        return value
    }

    function sqrt():Real64 {
        return value->(Real64).sqrt
    }

    function sin():Real64 {
        return value->(Real64).sin
    }

    function cos():Real64 {
        return value->(Real64).cos
    }

    function tan():Real64 {
        return value->(Real64).tan
    }

    ============================================================================
    Returns a single `Bit` from this integer. Index `0` is the least significant
    bit, and index `63` is the most significant bit.

    @param index the index of the bit to return
    @returns `true` if the bit is set
    ============================================================================
    @pre(index >= 0 & index < BITS)
    function [](index:Int):Bit {
        return value >> index && 1 = 1
    }

    ============================================================================
    Returns a slice of this integer's bit representation. Index `0` is the least 
    significant bit, and index `63` is the most significant bit. The sliced bits
    are returned packed in a new `UInt64` value. For example:

        var value := -1
        Console.writeLine(value[8 .. 16])

    This selects the second-least-signficant byte of `value`. Since `value` is
    `-1`, and the binary representation of `-1` has all bits set, the resulting
    slice also has all eight of its bits set, resulting in the value `255`.
    
    @param index the index of the bit to return
    @returns `true` if the bit is set
    ============================================================================
    @pre(left >= 0 & left < BITS & right >= 0 & right < BITS & step != 0)
    function [..](var left:Int?, var right:Int?, step:Int):UInt64 {
        if left = null {
            if step > 0
                left := 0
            else
                left := BITS - 1
        }
        if right = null {
            if step > 0
                right := BITS
            else
                right := -1
        }
        if step = 1 {
            var mask := (-1)->>(UInt64) >> (BITS - (right - left))
            return value >> left && mask
        }
        var count := 0
        var result:UInt64 := 0
        for i in left ... right by step {
            var bit := value[i]
            if bit
                result ||= 1 << count
            count += 1
        }
        return result
    }

    ============================================================================
    Returns the number of bits necessary to represent this number. For instance,
    the number `19` has binary representation `10011`, therefore `19.length` has
    the value `5`. Negative numbers always have their most significant bit set
    and thus a length equal to `BITS`, and `0` reports a length of `0`.
    ============================================================================
    @external
    function length():Int

    ============================================================================
    @hidden
    ============================================================================
    @protected
    @override
    function convertToString(thousandsSeparator:Char?):String {
        return convertToString(value, thousandsSeparator)
    }
    
    @override
    function =(o:Object):Bit {
        if o-?>(Integer)
            return value = o->(Number)->>(UInt64)
        else if o-?>(RealNumber)
            return value = o->(Number)->>(Real64)
        else
            return false
    }
        
    @override
    function ->>():Int8 {
        return value->>(Int8)
    }

    @override
    function ->>():Int16 {
        return value->>(Int16)
    }

    @override
    function ->>():Int32 {
        return value->>(Int32)
    }
        
    @override
    function ->>():Int64 {
        return value->>(Int64)
    }

    @override
    function ->>():UInt8 {
        return value->>(UInt8)
    }

    @override
    function ->>():UInt16 {
        return value->>(UInt16)
    }

    @override
    function ->>():UInt32 {
        return value->>(UInt32)
    }
        
    @override
    function ->>():UInt64 {
        return value
    }

    @override
    function ->>():Real32 {
        return value->>(Real32)
    }

    @override
    function ->>():Real64 {
        return value
    }

    @override
    function ->>():String {
        return value->>(String)
    }
}