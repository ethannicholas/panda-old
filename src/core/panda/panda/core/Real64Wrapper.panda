package panda.core

================================================================================
A 64 bit floating point number, capable of representing numbers in the 
approximate range -1.7 x 10^308 to 1.7 x 10^308 with 15 to 17 decimal digits of
precision.

`Real64`s are created automatically as the result of real literals (numbers 
appearing directly in the source code) and as the result of mathematical 
expressions.
================================================================================
@static
class Real64Wrapper : RealNumber {
    ============================================================================
    The `Real64` value closest to Euler's number, the base of the natural
    logarithm.
    ============================================================================
    constant E := 2.7182818284590452354
    
    ============================================================================
    The `Real64` value closest to pi, the ratio of a circle's circumference to 
    its diameter.
    ============================================================================
    constant PI := 3.14159265358979323846

    var value:Real64
    
    @class
    function [..](left:Real64, right:Real64):Array<Real64> {
        def result := new Array<Real64>()
        for v in left .. right
            result.append(v)
        return result
    }

    @class
    function [..](left:Real64, right:Real64, step:Real64):Array<Real64> {
        def result := new Array<Real64>()
        for v in left .. right by step
            result.append(v)
        return result
    }

    @class
    function [...](left:Real64, right:Real64):Array<Real64> {
        def result := new Array<Real64>()
        for v in left ... right
            result.append(v)
        return result
    }

    @class
    function [...](left:Real64, right:Real64, step:Real64):Array<Real64> {
        def result := new Array<Real64>()
        for v in left ... right by step
            result.append(v)
        return result
    }

    ============================================================================
    Given a pair of coordinates representing the point `(x, y)` on the 
    Cartesian plane, returns the angle in radians between the positive X axis
    and the point.
    ============================================================================
    @class
    @external
    function atan2(y:Real64, x:Real64):Real64

    constructor(value:Real64) {
        self.value := value
    }
        
    -- The operators below are *not actually used* by naive code; i.e. x * y
    -- will never call Real64::* (which is why none of these end up causing
    -- infinite recursion). They are specifically filtered out by 
    -- ASTGenerator::callOverloadedOperator, and defined here purely for
    -- convenience so that they can be accessed like "Real64::*".

    @$wrapperMethod
    function +(x:Real64):Real64 {
        return self + x
    }

    @$wrapperMethod
    function -(x:Real64):Real64 {
        return self - x
    }

    @$wrapperMethod
    function *(x:Real64):Real64 {
        return self * x
    }

    @$wrapperMethod
    function /(x:Real64):Real64 {
        return self / x
    }

    @$wrapperMethod
    function ^(x:Real64):Real64 {
        return self ^ x
    }

    @$wrapperMethod
    function >(x:Real64):Bit {
        return self > x
    }

    @$wrapperMethod
    function >=(x:Real64):Bit {
        return self >= x
    }

    @$wrapperMethod
    function <(x:Real64):Bit {
        return self < x
    }

    @$wrapperMethod
    function <=(x:Real64):Bit {
        return self <= x
    }

    @$wrapperMethod
    function =(x:Real64):Bit {
        return self = x
    }

    ============================================================================
    Returns the greater (closer to positive infinity) of itself and its 
    parameter.

    @param other the number to compare against
    @returns the maximum of itself and the other number
    ============================================================================
    @$wrapperMethod
    function max(other:Real64):Real64 {
        if self > other
            return value
        else
            return self
    }

    ============================================================================
    Returns the lesser (closer to negative infinity) of itself and its 
    parameter.

    @param other the number to compare against
    @returns the minimum of itself and the other number
    ============================================================================
    @$wrapperMethod
    function min(other:Real64):Real64 {
        if self < other
            return self
        else
            return other
    }

    @$wrapperMethod
    function sign():Int {
        if self > 0
            return 1
        else if self < 0
            return -1
        return 0
    }

    @$wrapperMethod
    function abs():Real64 {
        if self < 0
            return -self
        return self
    }

    @external
    @$wrapperMethod
    function isInfinite():Bit

    @external
    @$wrapperMethod
    function isNaN():Bit

    @external
    @$wrapperMethod
    function sqrt():Real64
    
    @external
    @$wrapperMethod
    function sin():Real64

    @external
    @$wrapperMethod
    function cos():Real64

    function tan():Real64 {
        return sin / cos
    }

    @protected
    @override
    function convertToString(thousandsSeparator:Char?):String {
        return convertToString(value, thousandsSeparator)
    }
    
    @override
    function =(o:Object):Bit {
        if o-?>(Number)
            return o->(Number)->>(Real64) = value
        return false
    }
        
    @override
    function ->>():Int8 {
        return value->>(Int8)
    }

    @override
    function ->>():Int16 {
        return value->>(Int16)
    }

    @override
    function ->>():Int32 {
        return value->>(Int32)
    }

    @override
    function ->>():Int64 {
        return value->>(Int64)
    }

    @override
    function ->>():Real32 {
        return value->>(Real32)
    }

    @override
    function ->>():UInt8 {
        return value->>(UInt8)
    }

    @override
    function ->>():UInt16 {
        return value->>(UInt16)
    }

    @override
    function ->>():UInt32 {
        return value->>(UInt32)
    }

    @override
    function ->>():UInt64 {
        return value->>(UInt64)
    }

    @override
    function ->>():Real64 {
        return value
    }

    @override
    function ->>():String {
        return value->>(String)
    }
}