package panda.core

================================================================================
A 64 bit floating point number, capable of representing numbers in the 
approximate range -1.7 x 10^308 to 1.7 x 10^308 with 15 to 17 decimal digits of
precision.

`Real64`s are created automatically as the result of real literals (numbers 
appearing directly in the source code) and as the result of mathematical 
expressions.
================================================================================
@final
class Real64Wrapper : RealNumber {
    ============================================================================
    The `Real64` value closest to Euler's number, the base of the natural
    logarithm.
    ============================================================================
    constant E := 2.7182818284590452354
    
    ============================================================================
    The `Real64` value closest to pi, the ratio of a circle's circumference to 
    its diameter.
    ============================================================================
    constant PI := 3.14159265358979323846

    ============================================================================
    @hidden
    ============================================================================
    def value:Real64
    
    ============================================================================
    Creates a slice of the reals: a list ranging from `left` (inclusive) to
    `right` (exclusive), counting by one.

    @param left the first number in the list
    @param right the last number in the list plus one
    @returns a list of numbers
    ============================================================================
    @class
    function [..](left:Real64, right:Real64):PrimitiveArray<Real64> {
        def result := new PrimitiveArray<Real64>()
        for v in left .. right
            result.append(v)
        return result
    }

    ============================================================================
    Creates a slice of the reals: a list ranging from `left` (inclusive) to
    `right` (exclusive), counting by `step`.

    @param left the first number in the list
    @param right the last number in the list
    @param step the step value
    @returns a list of numbers
    ============================================================================
    @class
    function [..](left:Real64, right:Real64, step:Real64):PrimitiveArray<Real64> {
        def result := new PrimitiveArray<Real64>()
        for v in left .. right by step
            result.append(v)
        return result
    }

    ============================================================================
    Creates a slice of the reals: a list ranging from `left` to `right` 
    (inclusive), counting by one.

    @param left the first number in the list
    @param right the last number in the list
    @returns a list of numbers
    ============================================================================
    @class
    function [...](left:Real64, right:Real64):PrimitiveArray<Real64> {
        def result := new PrimitiveArray<Real64>()
        for v in left ... right
            result.append(v)
        return result
    }

    ============================================================================
    Creates a slice of the reals: a list ranging from `left` to `right` 
    (inclusive), counting by `step`.

    @param left the first number in the list
    @param right the last number in the list
    @param step the step value
    @returns a list of numbers
    ============================================================================
    @class
    function [...](left:Real64, right:Real64, step:Real64):PrimitiveArray<Real64> {
        def result := new PrimitiveArray<Real64>()
        for v in left ... right by step
            result.append(v)
        return result
    }

    ============================================================================
    Given a pair of coordinates representing the point `(x, y)` on the 
    Cartesian plane, returns the angle in radians between the positive X axis
    and the point.

    @param x the x coordinate
    @param y the y coordinate
    @returns the angle in radians from the positive X axis to the point
    ============================================================================
    @class
    @external
    function atan2(y:Real64, x:Real64):Real64

    ============================================================================
    @hidden
    ============================================================================
    constructor(value:Real64) {
        self.value := value
    }
        
    -- The operators below are *not actually used* by naive code; i.e. x * y
    -- will never call Real64::* (which is why none of these end up causing
    -- infinite recursion). They are specifically filtered out by 
    -- ASTGenerator::callOverloadedOperator, and defined here purely for
    -- convenience so that they can be accessed like "Real64::*".

    ============================================================================
    The addition operator.

    @param x the number to add to this number
    @returns the sum of the two numbers
    ============================================================================
    @$wrapperMethod
    function +(x:Real64):Real64 {
        return self + x
    }

    ============================================================================
    The subtraction operator.

    @param x the number to subtract from this number
    @returns the difference of the two numbers
    ============================================================================
    @$wrapperMethod
    function -(x:Real64):Real64 {
        return self - x
    }

    ============================================================================
    The multiplication operator.

    @param x the number by which to multiply this number
    @returns the product of the two numbers
    ============================================================================
    @$wrapperMethod
    function *(x:Real64):Real64 {
        return self * x
    }

    ============================================================================
    The division operator.

    @param x the number by which to divide this number
    @returns the quotient of the two numbers
    ============================================================================
    @$wrapperMethod
    function /(x:Real64):Real64 {
        return self / x
    }

    ============================================================================
    The exponent operator.

    @param x the power to which to raise this number
    @returns the specified power of this number
    ============================================================================
    @$wrapperMethod
    function ^(x:Real64):Real64 {
        return self ^ x
    }

    ============================================================================
    The greater than operator.

    @param x the number to compare against
    @returns true if this number is greater than `x`
    ============================================================================
    @$wrapperMethod
    function >(x:Real64):Bit {
        return self > x
    }

    ============================================================================
    The greater than or equal to operator.

    @param x the number to compare against
    @returns true if this number is greater than or equal to `x`
    ============================================================================
    @$wrapperMethod
    function >=(x:Real64):Bit {
        return self >= x
    }

    ============================================================================
    The less than operator.

    @param x the number to compare against
    @returns true if this number is less than `x`
    ============================================================================
    @$wrapperMethod
    function <(x:Real64):Bit {
        return self < x
    }

    ============================================================================
    The less than or equal to operator.

    @param x the number to compare against
    @returns true if this number is less than or equal to `x`
    ============================================================================
    @$wrapperMethod
    function <=(x:Real64):Bit {
        return self <= x
    }

    ============================================================================
    Returns the greater (closer to positive infinity) of itself and its 
    parameter.

    @param other the number to compare against
    @returns the maximum of itself and the other number
    ============================================================================
    @$wrapperMethod
    function max(other:Real64):Real64 {
        if self > other
            return self
        else
            return other
    }

    ============================================================================
    Returns the lesser (closer to negative infinity) of itself and its 
    parameter.

    @param other the number to compare against
    @returns the minimum of itself and the other number
    ============================================================================
    @$wrapperMethod
    function min(other:Real64):Real64 {
        if self < other
            return self
        else
            return other
    }

    ============================================================================
    Returns a number describing the sign of this number. The result is `1` if
    this number is positive, `-1` if it is negative, and `0` if it is zero.

    @returns the sign of this number
    ============================================================================
    @$wrapperMethod
    function sign():Int {
        if self > 0
            return 1
        else if self < 0
            return -1
        return 0
    }

    ============================================================================
    Returns the absolute value of this number. The absolute value has the same
    magnitude, but is always positive.

    @returns the absolute value of this number
    ============================================================================
    @$wrapperMethod
    function abs():Real64 {
        if self < 0
            return -self
        return self
    }

    ============================================================================
    Returns `true` if this number represents positive or negative infinity.

    @returns `true` if this number is infinite
    ============================================================================
    @external
    @$wrapperMethod
    function isInfinite():Bit

    ============================================================================
    Returns `true` if this number represents a `NaN` (Not-A-Number) value.

    @returns `true` if this number is `NaN`
    ============================================================================
    @external
    @$wrapperMethod
    function isNaN():Bit

    ============================================================================
    Returns the square root of this number.

    @returns the square root of this number
    ============================================================================
    @external
    @$wrapperMethod
    function sqrt():Real64
    
    ============================================================================
    Returns the trigonometric sine of this number.

    @returns the sine of this number
    ============================================================================
    @external
    @$wrapperMethod
    function sin():Real64

    ============================================================================
    Returns the trigonometric cosine of this number.

    @returns the cosine of this number
    ============================================================================
    @external
    @$wrapperMethod
    function cos():Real64

    ============================================================================
    Returns the trigonometric tangent of this number.

    @returns the tangent of this number
    ============================================================================
    function tan():Real64 {
        return sin / cos
    }

    @protected
    @override
    function convertToString(thousandsSeparator:Char?, minWholeDigits:Int?, 
            minFractionDigits:Int?, maxFractionDigits:Int?, base:Int, 
            baseChars:String):String {
        return convertToString(value, thousandsSeparator, minWholeDigits,
                minFractionDigits, maxFractionDigits, base, baseChars)
    }
    
    @override
    function =(o:Object):Bit {
        if o-?>(Number)
            return o->(Number)->>(Real64) = value
        return false
    }
        
    ============================================================================
    Converts this number to an `Int8`.

    @returns this number as an `Int8`
    ============================================================================
    @override
    function ->>():Int8 {
        return value->>(Int8)
    }

    ============================================================================
    Converts this number to an `Int16`.

    @returns this number as an `Int16`
    ============================================================================
    @override
    function ->>():Int16 {
        return value->>(Int16)
    }

    ============================================================================
    Converts this number to an `Int32`.

    @returns this number as an `Int32`
    ============================================================================
    @override
    function ->>():Int32 {
        return value->>(Int32)
    }

    ============================================================================
    Converts this number to an `Int64`.

    @returns this number as an `Int64`
    ============================================================================
    @override
    function ->>():Int64 {
        return value->>(Int64)
    }

    ============================================================================
    Converts this number to a `UInt8`.

    @returns this number as a `UInt8`
    ============================================================================
    @override
    function ->>():UInt8 {
        return value->>(UInt8)
    }

    ============================================================================
    Converts this number to a `UInt16`.

    @returns this number as a `UInt16`
    ============================================================================
    @override
    function ->>():UInt16 {
        return value->>(UInt16)
    }

    ============================================================================
    Converts this number to a `UInt32`.

    @returns this number as a `UInt32`
    ============================================================================
    @override
    function ->>():UInt32 {
        return value->>(UInt32)
    }

    ============================================================================
    Converts this number to a `UInt64`.

    @returns this number as a `UInt64`
    ============================================================================
    @override
    function ->>():UInt64 {
        return value->>(UInt64)
    }

    ============================================================================
    Converts this number to a `Real32`.

    @returns this number as a `Real32`
    ============================================================================
    @override
    function ->>():Real32 {
        return value->>(Real32)
    }

    ============================================================================
    Returns this number.

    @returns this number
    ============================================================================
    @override
    function ->>():Real64 {
        return value
    }

    @override
    function ->>():String {
        return value->>(String)
    }
}