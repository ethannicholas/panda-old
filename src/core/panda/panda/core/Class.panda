package panda.core

================================================================================
Runtime representation of a Panda class. Every object stores its class as its 
`class` property, and classes may also be referred to in source code using the 
literal name of the class, e.g. `List<Object>`.
================================================================================
@final
class Class : Immutable {
    ============================================================================
    The class' name.
    ============================================================================
    def name:String
    
    ============================================================================
    The class' superclass.
    ============================================================================
    def superclass:Class
    
    -- true if instances contain pointers and thus need to be scanned by GC
    -- FIXME kill this. It's wrong and bad and I should feel bad.
    @private
    def containsPointers:Bit
    
    -- true if this class contains an overridden finalize() method and thus
    -- needs to be registered during allocation
    @private
    def overridesFinalize:Bit

    @private
    init() {
    }

    ============================================================================
    Returns the simple, package-less name of this class. For instance,
    `panda.core.Object` has the simple name `"Object"`.

    @returns the simple name of the class
    ============================================================================
    property simpleName:String

    @private
    function get_simpleName():String {
        var lastDot := name.lastIndexOf(".")
        if lastDot != null {
            return name[lastDot + 1..]
        }
        else {
            return name
        }
    }

    @override
    function convert():String {
        return name
    }
}