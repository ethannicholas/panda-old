package panda.core

================================================================================
A 16 bit Unicode code point, representing a character in the Basic Multilingual
Plane.
================================================================================
@final
class CharWrapper : Primitive {
    ============================================================================
    The minimum value representable by a `Char`.
    ============================================================================
    constant MIN := 0->(Char)

    ============================================================================
    The maximum value representable by a `Char`.
    ============================================================================
    constant MAX := 0xFFFF->(Char)

    ============================================================================
    @hidden
    ============================================================================
    def value:Char

    ============================================================================
    Creates a slice of the chars: a list ranging from `left` to [MAX].

    @param left the first character in the list
    @returns a list of characters
    ============================================================================
    @class
    function [..](left:Char):Array<Char> {
        return Char[left...]
    }

    ============================================================================
    Creates a slice of the chars: a list ranging from `left` (inclusive) to
    `right` (exclusive).

    @param left the first character in the list
    @param right the last character in the list plus one
    @returns a list of characters
    ============================================================================
    @class
    function [..](left:Char, right:Char):Array<Char> {
        def result := new Array<Char>()
        for v:Char in left .. right
            result.append(v)
        return result
    }

    ============================================================================
    Creates a slice of the chars: a list ranging from `left` (inclusive) to
    `right` (exclusive), counting by `step`.

    @param left the first character in the list
    @param right the last character in the list plus one
    @param step the step value
    @returns a list of characters
    ============================================================================
    @class
    function [..](left:Char, right:Char, step:Int16):Array<Char> {
        def result := new Array<Char>()
        for v:Char in left .. right by step
            result.append(v)
        return result
    }

    ============================================================================
    Creates a slice of the chars: a list ranging from `left` to [MAX].

    @param left the first character in the list
    @returns a list of characters
    ============================================================================
    @class 
    function [...](left:Char):Array<Char> {
        throw new NotSupportedException()
    }
    
    ============================================================================
    Creates a slice of the chars: a list ranging from `left` to `right` 
    (inclusive).

    @param left the first character in the list
    @param right the last character in the list
    @returns a list of characters
    ============================================================================
    @class
    function [...](left:Char, right:Char):Array<Char> {
        var result := new Array<Char>()
        for v:Char in left ... right
            result.append(v)
        return result
    }

    ============================================================================
    Creates a slice of the chars: a list ranging from `left` to `right` 
    (inclusive), counting by `step`.

    @param left the first character in the list
    @param right the last character in the list
    @param step the step value
    @returns a list of characters
    ============================================================================
    @class
    function [...](left:Char, right:Char, step:Int16):Array<Char> {
        def result := new Array<Char>()
        for v:Char in left ... right by step
            result.append(v)
        return result
    }

    ============================================================================
    @hidden
    ============================================================================
    constructor(value:Char) {
        self.value := value
    }

    ============================================================================
    Returns `true` if this is a whitespace character.

    @returns `true` if this is a whitespace character
    ============================================================================
    function isWhitespace():Bit {
        -- FIXME do this for real when we have Unicode support
        return "\n\r\t ".contains(value)
    }

    ============================================================================
    Returns a string consisting of a number of copies of this character.

    @param n the number of copies
    @returns a string consisting of `n` copies of this character
    ============================================================================
    @$wrapperMethod
    function *(n:Int):String {
        return self->>(String) * n
    }
        
    ============================================================================
    Returns a string consisting of a number of copies of the character.

    @param n the number of copies
    @param c the character to copy
    @returns a string consisting of `n` copies of this character
    ============================================================================
    @class
    function *(n:Int, c:Char):String {
        return c->>(String) * n
    }

    @override
    function =(o:Object):Bit {
        return o-?>(CharWrapper) & value = o->(CharWrapper).value |
                o-?>(String) & value->>(String) = o
    }

    ============================================================================
    Returns a single-character string containing this character.

    @returns this character converted to a string
    ============================================================================
    @override
    function ->>():String {
        return value->>(String)
    }
      
    ============================================================================
    @hidden
    ============================================================================
    function ->>():Char {
        return value
    }

    @override
    @preOr(fmt = "" | fmt = "panda")
    function format(fmt:String):String {
        if fmt = ""
            return value->>(String)
        else
            return value->>(String).format(fmt)
    }
}