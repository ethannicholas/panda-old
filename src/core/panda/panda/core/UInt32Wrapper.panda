package panda.core

================================================================================
A 32 bit unsigned integer, capable of representing numbers in the range 0 to 
4,294,967,295.

`UInt32`s are created automatically as the result of integer literals (numbers 
appearing directly in the source code) and as the result of mathematical 
expressions.
================================================================================
@static
class UInt32Wrapper : UnsignedInteger {
    ============================================================================
    The minimum value representable by a `UInt32`.
    ============================================================================
    constant MIN:UInt32 := 0
    
    ============================================================================
    The maximum value representable by a `UInt32`.
    ============================================================================
    constant MAX:UInt32 := 4294967295

    ============================================================================
    The number of bits in a `UInt32`.
    ============================================================================
    constant BITS := 32

    ============================================================================
    @hidden
    ============================================================================
    def value:UInt32
    
    ============================================================================
    Creates a slice of the integers: a list ranging from `left` to [MAX].

    @param left the first number in the list
    @returns a list of numbers
    ============================================================================
    @class
    function [..](left:UInt32):Array<UInt32> {
        return UInt32[left...]
    }
    
    ============================================================================
    Creates a slice of the integers: a list ranging from `left` (inclusive) to
    `right` (exclusive).

    @param left the first number in the list
    @param right the last number in the list plus one
    @returns a list of numbers
    ============================================================================
    @class
    function [..](left:UInt32, right:UInt32):Array<UInt32> {
        def result := new Array<UInt32>()
        for v:UInt32 in left .. right
            result.append(v)
        return result
    }

    ============================================================================
    Creates a slice of the integers: a list ranging from `left` (inclusive) to
    `right` (exclusive), counting by `step`.

    @param left the first number in the list
    @param right the last number in the list plus one
    @param step the step value
    @returns a list of numbers
    ============================================================================
    @class
    function [..](left:UInt32, right:UInt32, step:Int32):Array<UInt32> {
        def result := new Array<UInt32>()
        for v:Int64 in left .. right by step
            result.append(v->(UInt32))
        return result
    }

    ============================================================================
    Creates a slice of the integers: a list ranging from `left` to [MAX].

    @param left the first number in the list
    @returns a list of numbers
    ============================================================================
    @class 
    function [...](left:UInt32):Array<UInt32> {
        throw new UnimplementedException()
    }
    
    ============================================================================
    Creates a slice of the integers: a list ranging from `left` to `right` 
    (inclusive).    

    @param left the first number in the list
    @param right the last number in the list
    @returns a list of numbers
    ============================================================================
    @class
    function [...](left:UInt32, right:UInt32):Array<UInt32> {
        var result := new Array<UInt32>()
        for v:UInt32 in left ... right
            result.append(v)
        return result
    }

    ============================================================================
    Creates a slice of the integers: a list ranging from `left` to `right` 
    (inclusive), counting by `step`.

    @param left the first number in the list
    @param right the last number in the list
    @param step the step value
    @returns a list of numbers
    ============================================================================
    @class
    function [...](left:UInt32, right:UInt32, step:Int32):Array<UInt32> {
        def result := new Array<UInt32>()
        for v:Int64 in left ... right by step
            result.append(v->(UInt32))
        return result
    }

    ============================================================================
    @hidden
    ============================================================================
    constructor(value:UInt32) {
        self.value := value
    }
        
    -- The operators below are *not actually used* by naive code; i.e. x * y
    -- will never call UInt32::* (which is why none of these end up causing
    -- infinite recursion). They are specifically filtered out by 
    -- ASTGenerator::callOverloadedOperator, and defined here purely for
    -- convenience so that they can be accessed like "UInt32::*".

    ============================================================================
    The addition operator.

    @param x the number to add to this number
    @returns the sum of the two numbers
    ============================================================================
    @$wrapperMethod
    function +(x:UInt32):UInt32 {
        try {
            return self + x
        }
        catch e:CastError {
            throw new OverflowError(e.message)
        }
    }

    ============================================================================
    The subtraction operator.

    @param x the number to subtract from this number
    @returns the difference of the two numbers
    ============================================================================
    @$wrapperMethod
    function -(x:UInt32):UInt32 {
        try {
            return self - x
        }
        catch e:CastError {
            throw new OverflowError(e.message)
        }
    }

    ============================================================================
    The multiplication operator.

    @param x the number by which to multiply this number
    @returns the product of the two numbers
    ============================================================================
    @$wrapperMethod
    function *(x:UInt32):UInt32 {
        try {
            return self * x
        }
        catch e:CastError {
            throw new OverflowError(e.message)
        }
    }

    ============================================================================
    The division operator.

    @param x the number by which to divide this number
    @returns the quotient of the two numbers
    ============================================================================
    @$wrapperMethod
    function /(x:UInt32):Real32 {
        try {
            return self / x
        }
        catch e:CastError {
            throw new OverflowError(e.message)
        }
    }

    ============================================================================
    The integer division operator.

    @param x the number by which to divide this number
    @returns the quotient of the two numbers
    ============================================================================
    @$wrapperMethod
    function //(x:UInt32):UInt32 {
        try {
            return self // x
        }
        catch e:CastError {
            throw new OverflowError(e.message)
        }
    }

    ============================================================================
    The exponent operator.

    @param x the power to which to raise this number
    @returns the specified power of this number
    ============================================================================
    @$wrapperMethod
    function ^(x:UInt32):UInt32 {
        try {
            return self ^ x
        }
        catch e:CastError {
            throw new OverflowError(e.message)
        }
    }

    ============================================================================
    The exponent operator.

    @param x the power to which to raise this number
    @returns the specified power of this number
    ============================================================================
    @$wrapperMethod
    function ^(x:Real32):Real32 {
        try {
            return self ^ x
        }
        catch e:CastError {
            throw new OverflowError(e.message)
        }
    }

    ============================================================================
    The bitwise OR operator.

    @param x the number to OR with this number
    @returns the OR of the two numbers
    ============================================================================
    @$wrapperMethod
    function ||(x:UInt32):UInt32 {
        try {
            return self || x
        }
        catch e:CastError {
            throw new OverflowError(e.message)
        }
    }

    ============================================================================
    The bitwise AND operator.

    @param x the number to AND with this number
    @returns the AND of the two numbers
    ============================================================================
    @$wrapperMethod
    function &&(x:UInt32):UInt32 {
        try {
            return self && x
        }
        catch e:CastError {
            throw new OverflowError(e.message)
        }
    }

    ============================================================================
    The bitwise XOR operator.

    @param x the number to XOR with this number
    @returns the XOR of the two numbers
    ============================================================================
    @$wrapperMethod
    function ~~(x:UInt32):UInt32 {
        try {
            return self ~~ x
        }
        catch e:CastError {
            throw new OverflowError(e.message)
        }
    }

    ============================================================================
    The bitwise NOT operator.

    @returns the bitwise complement of this number
    ============================================================================
    @$wrapperMethod
    function !!():UInt32 {
        return !!self
    }

    ============================================================================
    The greater than operator.

    @param x the number to compare against
    @returns true if this number is greater than `x`
    ============================================================================
    @$wrapperMethod
    function >(x:UInt32):Bit {
        return self > x
    }

    ============================================================================
    The greater than or equal to operator.

    @param x the number to compare against
    @returns true if this number is greater than or equal to `x`
    ============================================================================
    @$wrapperMethod
    function >=(x:UInt32):Bit {
        return self >= x
    }

    ============================================================================
    The less than operator.

    @param x the number to compare against
    @returns true if this number is less than `x`
    ============================================================================
    @$wrapperMethod
    function <(x:UInt32):Bit {
        return self < x
    }

    ============================================================================
    The less than or equal to operator.

    @param x the number to compare against
    @returns true if this number is less than or equal to `x`
    ============================================================================
    @$wrapperMethod
    function <=(x:UInt32):Bit {
        return self <= x
    }

    ============================================================================
    Returns the greater (closer to positive infinity) of itself and its 
    parameter.

    @param other the number to compare against
    @returns the maximum of itself and the other number
    ============================================================================
    @$wrapperMethod
    function max(other:Int64):Int64 {
        if self > other
            return self
        else
            return other
    }

    ============================================================================
    Returns the greater (closer to positive infinity) of itself and its 
    parameter.

    @param other the number to compare against
    @returns the maximum of itself and the other number
    ============================================================================
    @$wrapperMethod
    function max(other:UInt32):UInt32 {
        if self > other
            return self
        else
            return other
    }

    ============================================================================
    Returns the greater (closer to positive infinity) of itself and its 
    parameter.

    @param other the number to compare against
    @returns the maximum of itself and the other number
    ============================================================================
    @$wrapperMethod
    function max(other:UInt64):UInt64 {
        if self > other
            return self
        else
            return other
    }

    ============================================================================
    Returns the greater (closer to positive infinity) of itself and its 
    parameter.

    @param other the number to compare against
    @returns the maximum of itself and the other number
    ============================================================================
    @$wrapperMethod
    function max(other:Real32):Real32 {
        if self > other
            return self
        else
            return other
    }

    ============================================================================
    Returns the greater (closer to positive infinity) of itself and its 
    parameter.

    @param other the number to compare against
    @returns the maximum of itself and the other number
    ============================================================================
    @$wrapperMethod
    function max(other:Real64):Real64 {
        if self > other
            return self
        else
            return other
    }

    ============================================================================
    Returns the lesser (closer to negative infinity) of itself and its 
    parameter.

    @param other the number to compare against
    @returns the minimum of itself and the other number
    ============================================================================
    @$wrapperMethod
    function min(other:Int64):Int64 {
        if self < other
            return self
        else
            return other
    }

    ============================================================================
    Returns the lesser (closer to negative infinity) of itself and its 
    parameter.

    @param other the number to compare against
    @returns the minimum of itself and the other number
    ============================================================================
    @$wrapperMethod
    function min(other:UInt32):UInt32 {
        if self < other
            return self
        else
            return other
    }

    ============================================================================
    Returns the lesser (closer to negative infinity) of itself and its 
    parameter.

    @param other the number to compare against
    @returns the minimum of itself and the other number
    ============================================================================
    @$wrapperMethod
    function min(other:UInt64):UInt64 {
        if self < other
            return self
        else
            return other
    }

    ============================================================================
    Returns the lesser (closer to negative infinity) of itself and its 
    parameter.

    @param other the number to compare against
    @returns the minimum of itself and the other number
    ============================================================================
    @$wrapperMethod
    function min(other:Real32):Real32 {
        if self < other
            return self
        else
            return other
    }

    ============================================================================
    Returns the lesser (closer to negative infinity) of itself and its 
    parameter.

    @param other the number to compare against
    @returns the minimum of itself and the other number
    ============================================================================
    @$wrapperMethod
    function min(other:Real64):Real64 {
        if self < other
            return self
        else
            return other
    }

    ============================================================================
    Returns the square root of this number. 

    @returns the square root of this number
    ============================================================================
    @$wrapperMethod
    function sqrt():Real32 {
        return self->(Real32).sqrt
    }

    ============================================================================
    Returns the trigonometric sine of this number.

    @returns the sine of this number
    ============================================================================
    @$wrapperMethod
    function sin():Real32 {
        return self->(Real32).sin
    }

    ============================================================================
    Returns the trigonometric cosine of this number.

    @returns the cosine of this number
    ============================================================================
    @$wrapperMethod
    function cos():Real32 {
        return self->(Real32).cos
    }

    ============================================================================
    Returns the trigonometric tangent of this number.

    @returns the tangent of this number
    ============================================================================
    @$wrapperMethod
    function tan():Real32 {
        return self->(Real32).tan
    }

    ============================================================================
    Returns a single `Bit` from this integer. Index `0` is the least significant
    bit, and index `31` is the most significant bit.

    @param index the index of the bit to return
    @returns `true` if the bit is set
    ============================================================================
    @pre(index >= 0 & index < BITS)
    function [](index:Int):Bit {
        return value >> index && 1 = 1
    }

    ============================================================================
    Returns a slice of this integer's bit representation. Index `0` is the least 
    significant bit, and index `31` is the most significant bit. The sliced bits
    are returned packed in a new `UInt32` value. For example:

        var value := UInt32.MAX
        Console.writeLine(value[8 .. 16])

    This selects the second-least-signficant byte of `value`. Since `value` is
    `UInt32.MAX`, and the binary representation of `UInt32.MAX` has all bits 
    set, the resulting slice also has all eight of its bits set, resulting in 
    the value `255`.
    
    @param left the index of the first bit to return
    @param right the index of the last bit to return
    @param step the number of bits to count by
    @returns the selected bits
    ============================================================================
    @pre(left >= 0 & left < BITS & right >= 0 & right < BITS & step != 0)
    function [..](var left:Int?, var right:Int?, step:Int):UInt32 {
        if left = null {
            if step > 0
                left := 0
            else
                left := BITS - 1
        }
        if right = null {
            if step > 0
                right := BITS
            else
                right := -1
        }
        if step = 1 {
            var mask := -1->>(UInt32) >> (BITS - (right - left))
            return value >> left && mask
        }
        var count := 0
        var result:UInt32 := 0
        for i in left ... right by step {
            var bit := value[i]
            -- FIXME unnecessary cast
            if bit
                result ||= (1 << count)->(UInt32)
            count += 1
        }
        return result
    }

    ============================================================================
    Returns the number of bits necessary to represent this number. For instance,
    the number `19` has binary representation `10011`, therefore `19.length` has
    the value `5`. The number `0` reports a length of `0`.

    @returns the length of the number's binary representation
    ============================================================================
    @external
    function length():Int

    ============================================================================
    @hidden
    ============================================================================
    @protected
    @override
    function convertToString(thousandsSeparator:Char?, minWholeDigits:Int?, 
            minFractionDigits:Int?, maxFractionDigits:Int?, base:Int, 
            baseChars:String):String {
        return convertToString(value, thousandsSeparator, minWholeDigits,
                minFractionDigits, maxFractionDigits, base, baseChars)
    }
    
    @override
    function =(o:Object):Bit {
        if o-?>(Integer)
            return value = o->(Number)->>(Int64)
        else if o-?>(RealNumber)
            return value = o->(Number)->>(Real64)
        else
            return false
    }
        
    ============================================================================
    Coerces this number into an `Int8`. If it is out of range, the result will
    be the low 8 bits of this number.

    @returns this number as an `Int8`
    ============================================================================
    @override
    function ->>():Int8 {
        return value->>(Int8)
    }

    ============================================================================
    Coerces this number into an `Int16`. If it is out of range, the result will
    be the low 16 bits of this number.

    @returns this number as an `Int16`
    ============================================================================
    @override
    function ->>():Int16 {
        return value->>(Int16)
    }

    ============================================================================
    Coerces this number into an `Int32`. If it is out of range, the result will
    be negative due to overflow.

    @returns this number as an `Int32`
    ============================================================================
    @override
    function ->>():Int32 {
        return value->>(Int32)
    }

    ============================================================================
    Converts this number to an `Int64`.

    @returns this number as an `Int64`
    ============================================================================
    @override
    function ->>():Int64 {
        return value
    }

    ============================================================================
    Coerces this number into a `UInt8`. If it is out of range, the result will
    be the low 8 bits of this number.

    @returns this number as a `UInt8`
    ============================================================================
    @override
    function ->>():UInt8 {
        return value->>(UInt8)
    }

    ============================================================================
    Coerces this number into a `UInt16`. If it is out of range, the result will
    be the low 16 bits of this number.

    @returns this number as a `UInt16`
    ============================================================================
    @override
    function ->>():UInt16 {
        return value->>(UInt16)
    }

    ============================================================================
    Returns this number.

    @returns this number
    ============================================================================
    @override
    function ->>():UInt32 {
        return value
    }

    ============================================================================
    Converts this number to a `UInt64`.

    @returns this number as a `UInt64`
    ============================================================================
    @override
    function ->>():UInt64 {
        return value
    }

    ============================================================================
    Converts this number to a `Real32`.

    @returns this number as a `Real32`
    ============================================================================
    @override
    function ->>():Real32 {
        return value
    }

    ============================================================================
    Converts this number to a `Real64`.

    @returns this number as a `Real64`
    ============================================================================
    @override
    function ->>():Real64 {
        return value
    }

    @override
    function ->>():String {
        return value->>(String)
    }    
}