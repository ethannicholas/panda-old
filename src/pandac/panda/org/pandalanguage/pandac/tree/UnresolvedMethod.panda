package org.pandalanguage.pandac.tree

uses org.pandalanguage.pandac.compiler.Context
uses org.pandalanguage.pandac.compiler.Dummy
uses org.pandalanguage.pandac.stubs.MethodStub
uses org.pandalanguage.pandac.types.UnresolvedType
uses org.pandalanguage.pandac.types.Type

================================================================================
Represents a method name which has not yet been resolved to a specific overload.
================================================================================
class UnresolvedMethod : UnresolvedValue {
    def target:Value?

    def methods:ListView<MethodStub>

    @pre(methods.length > 1)
    init(position:Position, target:Value, 
            methods:ListView<MethodStub>) {
        super.init(position, "ambiguous reference to method '\{methods[0].name}'", 
                null)
        self.target := target
        self.methods := methods
    }

    @override
    method implicitCastCost(context:Context, t:Type):Int? {
        var best := Array<MethodStub>()
        var bestCost := Int.MAX
        for m in methods {
            def cost := m.type(context).implicitCastCost(context, t)
            if cost = null
                continue
            if cost < bestCost
                bestCost := cost
        }
        if bestCost = Int.MAX
            return null
        return bestCost
    }

    @override
    method implicitCast(position:Position, context:Context, t:Type):Value {
        var best := Array<MethodStub>()
        var bestCost := Int.MAX
        for m in methods {
            def cost := m.type(context).implicitCastCost(context, t)
            if cost = null
                continue
            if cost < bestCost {
                best := [m]
                bestCost := cost
            }
            else if cost = best 
                best.add(m)
        }
        if best.length = 0 {
            context.errors.error("expected '\{t}', but none of the overloads " +
                    "of '\{methods[0].name}' match", position)
            return Dummy(position, t)
        }
        else if best.length = 1 {
            return MethodReference(context, position, target, 
                    best[0]).implicitCast(position, context, t)
        }
        else {
            reportError(context, best)
            return Dummy(position, t)
        }
    }    

    @override
    method canExplicitCastTo(context:Context, t:Type):Bit {
        for m in methods {
            if m.type(context).canExplicitCastTo(context, t)
                return true
        }
        return false
    }

    @override
    method explicitCast(position:Position, context:Context, t:Type):Value {
        def matches := Array<MethodStub>()
        for m in methods {
            if m.type(context).canExplicitCastTo(context, t)
                matches.add(m)
        }
        if matches.length = 0 {
            context.errors.error("expected '\{t}', but none of the overloads " +
                    "of '\{methods[0].name}' match", position)
            return Dummy(position, t)
        }
        if matches.length = 1 {
            return MethodReference(context, position, target, 
                    matches[0]).explicitCast(position, context, t)
        }
        else {
            reportError(context, matches)
            return Dummy(position, t)
        }
    }    

    @override
    method reportError(context:Context, matches:ListView<MethodStub>) {
        def text := MutableString("ambiguous reference to method " +
                "'\{methods[0].name}', the following methods are all equally " +
                "good matches:\n")
        for m in matches
            text.append("    \{m} (\{m.position})\n")
        context.errors.error(text.convert(), position)
    }

    @override
    method reportError(context:Context) {
        reportError(context, methods)
    }

   @override
    function convert():String {
        return "\{methods[0].name}"
    }
}