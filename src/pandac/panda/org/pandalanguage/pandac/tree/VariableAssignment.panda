package org.pandalanguage.pandac.tree

class VariableAssignment : Value {
    def variable:Variable

    def value:Value

    ============================================================================
    `true` if this assignment represents the initial value of a variable (as in
    `def x := 3`).

    We "really" allocate all variables at the beginning of the method, so:

        method foo() {
            loop {
                var line := Console.readLine()
                ...
            }
        }

    compiles as if it were written:

        method foo() {
            var line:String?
            loop {
                line := Console.readLine()
                ...
            }
        }

    This is so we don't try to allocate more stack storage every iteration 
    through the loop. Unfortunately, now we can't tell just by looking at the
    code if our usage of `def`s is legal -- they are all allocated at the
    beginning, and then might be assigned to once every iteration through a
    loop. We could get all fancy in checking scoping and all that to determine
    if the assignments are legal, but it's a lot easier just to keep track of
    which one was the initial assignment and treat it as always legal.
    ============================================================================
    def isInitialValue:Bit

    constructor(position:Position, v:Variable, value:Value) {
        constructor(position, v, value, false)
    }

    @pre(value.type = v.type)
    constructor(position:Position, v:Variable, value:Value, 
            isInitialValue:Bit) {
        super.constructor(position, value.type)
        self.variable := v
        self.value := value
        self.isInitialValue := isInitialValue
    }
    
    @override
    function format(fmt:String):String {
        return "(" + variable + " := " + value + ")"
    }
}