package org.pandalanguage.pandac.tree

uses org.pandalanguage.pandac.compiler.Context
uses org.pandalanguage.pandac.compiler.Dummy
uses org.pandalanguage.pandac.compiler.ErrorReporter
uses org.pandalanguage.pandac.stubs.MethodStub
uses org.pandalanguage.pandac.types.PrimitiveArrayType
uses org.pandalanguage.pandac.types.ClassType
uses org.pandalanguage.pandac.types.StringOrCharType
uses org.pandalanguage.pandac.types.Type
uses org.pandalanguage.pandac.types.UnresolvedType

================================================================================
Represents an index or slice which has not yet been fully resolved. This is used
(as opposed to immediately returning a finished [Value]) to make overloaded
assignments easier. When processing a[1], we return an `UnresolvedIndex` to 
represent it, so if the next token is `:=` we can easily perform the appropriate 
lookup.
================================================================================
class UnresolvedIndex : UnresolvedValue {
    def contextValue:Value
    
    def index:Value

    constructor(context:Context, position:Position, contextValue:Value, 
            index:Value) {
        super.constructor(position, "<unimplemented index operator>",
                context.callOverloadedOperator(position, "[]", contextValue, 
                    index))
        self.contextValue := contextValue
        self.index := index
    }

    @override
    method reportError(context:Context) {
        context.errors.error("'\{contextValue.type.displayName}' does " + 
                "not implement the '[]' operator for " +
                "'\{index.type.displayName}'", position)
    }

    @override
    function ->>():String {
        return contextValue + "[" + index + "]"
    }
}