package org.pandalanguage.pandac.tree

uses org.pandalanguage.pandac.stubs.MethodStub

================================================================================
Represents a method call.
================================================================================
class MethodCall : Value {
    ============================================================================
    The method being called.
    ============================================================================
    def methodStub:MethodStub

    ============================================================================
    The method parameters.
    ============================================================================
    def parameters:Array<Value>

    ============================================================================
    True if the method was called on 'super'.
    ============================================================================
    def isSuper:Bit

    ============================================================================
    Creates a new `MethodCall`.

    @param position the call's position
    @param methodStub the method being called
    @param isSuper `true` if this is a call to a superclass method
    @param params the parameters to the method (including 'self', if applicable)
    ============================================================================
--    @pre(parameters.length = methodStub.parameters.length)
--    @pre(valid(methodStub, parameters))
    constructor(position:Position, methodStub:MethodStub, isSuper:Bit, 
            parameters:ListView<Value>) {
        super.constructor(position, methodStub.returnType)
        self.methodStub := methodStub
        self.isSuper := isSuper
        self.parameters := new Array<Value>(parameters)
    }

    @private
    @class
    function valid(m:MethodStub, parameters:ListView<Value>):Bit {
        for i, p in m.parameters {
            if p.type != parameters[i].type
                return false
        }
        return true
    }

    @override
    function format(fmt:String):String {
        def result := new MutableString()
        if methodStub.annotations.isClass {
            result.append(methodStub.owner.name)
            result.append(".")
            result.append(methodStub.name)
            result.add("(")
            for i, p in parameters {
                if i > 0
                    result.append(", ")
                result.append(p)
            }
            result.add(")")
        }
        else {
            if parameters[0]->>(String) != "self" {
                if isSuper
                    result.append("super.")
                else {
                    result.append(parameters[0])
                    result.append(".")
                }
            }
            result.append(methodStub.name)
            result.add("(")
            for i, p in parameters {
                if i = 0
                    continue
                if i > 1
                    result.append(", ")
                result.append(p)
            }
            result.add(")")        
        }
        return result->>(String)
    }
}