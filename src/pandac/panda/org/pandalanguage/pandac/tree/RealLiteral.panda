package org.pandalanguage.pandac.tree

uses org.pandalanguage.pandac.compiler.Context
uses org.pandalanguage.pandac.compiler.Message
uses org.pandalanguage.pandac.types.RealLiteralType
uses org.pandalanguage.pandac.types.RealType
uses org.pandalanguage.pandac.types.Type

class RealLiteral : Value {
    def value:Real64
    
    constructor(position:Position, value:Real64) {
        constructor(position, value, RealLiteralType.REAL_LITERAL)
    }
    
    @pre(type.isReal)
    constructor(position:Position, value:Real64, type:Type) {
        super.constructor(position, type)
        self.value := value
    }

    @override
    method implicitCast(position:Position, context:Context, t:Type):Value {
        switch t {
            case RealType.REAL32, RealType.REAL64:
                return new RealLiteral(position, value, t)
            default: return super.implicitCast(position, context, t)
        }    
    }

    @override
    function format(fmt:String):String {
        return value->>(String)
    }
}