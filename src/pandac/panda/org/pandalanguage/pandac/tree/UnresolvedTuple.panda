package org.pandalanguage.pandac.tree

uses org.pandalanguage.pandac.types.PrimitiveArrayType
uses org.pandalanguage.pandac.types.ClassType
uses org.pandalanguage.pandac.types.TupleType
uses org.pandalanguage.pandac.types.Type
uses org.pandalanguage.pandac.types.UnresolvedType

================================================================================
Represents a tuple literal. Tuple literals are turned into concrete tuple 
instances (via a Construct) once their types are determined.
================================================================================
class UnresolvedTuple : Value {
    constructor(position:Position, values:ListView<Value>) {
        super.constructor(position, getType(position, values), remap(values))
    }

    -- FIXME covariance should take care of this when it's in
    @private
    @class
    function remap(children:ListView<Value>):ListView<Node> {
        def result := new Array<Node>()
        for c in children
            result.add(c)
        return result
    }

    @private
    @class
    function getType(position:Position, values:ListView<Value>):Type {
        def name := new MutableString("(")
        for i, v in values {
            if i > 0
                name.append(", ")
            name.append(v.type.displayName)
        }
        return new UnresolvedType(name + ")", position)
    }

    function values():ListView<Value> {
        def result := new Array<Value>()
        for v in self
            result.add(v->(Value))
        return result
    }
}