package org.pandalanguage.pandac.tree

class Block : Statement {
    def statements:List<Statement>

    init(position:Position, statements:ListView<Statement>) {
        super.init(position)
        self.statements := Array<Statement>(statements)
    }

    -- this method was originally written for LLVMCodeGenerator to know whether
    -- a branch was necessary at the end of a block. Throw is not considered a
    -- branch for that reason, because it's a function call rather than an 
    -- LLVM-level branch. This eventually led to the creation of 
    -- endsInBranchOrThrow, which is just gross. Should probably move this 
    -- LLVM-specific test into LLVMCodeGenerator and rename endsInBranchOrThrow 
    -- to endsInBranch.
    function endsInBranch():Bit {
        if statements.length = 0
            return false
        var last := statements[statements.length - 1]
        return last-?>Return | last-?>Continue | last-?>Break |
                (last-?>Block & last->Block.endsInBranch())
    }

    function endsInBranchOrThrow():Bit {
        if statements.length = 0
            return false
        var last := statements[statements.length - 1]
        return last-?>Return | last-?>Continue | last-?>Break | 
                last-?>Throw | last-?>Unreachable |
                (last-?>Block & last->Block.endsInBranchOrThrow())
    }

    @override
    function convert():String {
        return "{ " + statements.join("\n") + " }"
    }
}