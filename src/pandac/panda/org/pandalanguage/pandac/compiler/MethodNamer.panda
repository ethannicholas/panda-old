package org.pandalanguage.pandac.compiler

uses org.pandalanguage.pandac.stubs.ClassStub
uses org.pandalanguage.pandac.stubs.MethodStub
uses org.pandalanguage.pandac.types.Type

================================================================================
Applies a pluggable naming strategy to all of the methods in a class, in order
to name methods in a fashion which is compatible with and unambiguous in the
target language.
================================================================================
class MethodNamer {
    @private
    def named := new HashSet<ClassStub>()

    @class
    function escapeTypeName(name:String):String {
        return name.replace(".", "$").replace("<", "$LT").
                replace(">", "$GT").replace("?", "$Z").
                replace("*", "$p").replace("(", "$LP").
                replace(")", "$RP").replace("=", "$EQ").
                replace(",", "$C").replace("&", "$AM")
    }

    method nameMethods(stubs:MapView<String, ClassStub>,
            strategy:MethodNamingStrategy):
            MapView<MethodStub, String> {
        def result := new HashMap<MethodStub, String>()
        for cl in stubs.values
            nameMethods(cl, stubs, strategy, result)
        return result
    }

    @private
    @limited
    method nameMethods(cl:ClassStub, stubs:MapView<String, ClassStub>,
            strategy:MethodNamingStrategy, result:Map<MethodStub, String>) {
        if named.contains(cl)
            return
        named.add(cl)
        def getStub := function(type:Type):ClassStub {
            def result := stubs[type.name]
            if result != null { -- FIXME remove this test when compiler is finished
                assert result != null : "no stub for \{type.name}"
                return result
            }
            else {
                Panda.log("no stub for " + type)
                return stubs["panda.core.Object"]->(ClassStub)
            }
        }
        def sc := cl.superclass
        if sc != null
            nameMethods(getStub(sc), stubs, strategy, result)
        for intf in cl.interfaces
            nameMethods(getStub(intf), stubs, strategy, result)
        def disambiguationBuckets:Map<String, List<MethodStub>> := 
                new HashMap<String, List<MethodStub>>()
        for m in cl.methods {
            if !cl.annotations.isUnspecified | m.annotations.isClass {
                Console.writeLine("naming \{cl.name}::\{m}")
                addToBucket(disambiguationBuckets, strategy.disambiguation(m), 
                        m)
            }
        }
        for (_, bucket) in disambiguationBuckets {
            def shortBuckets:Map<String, List<MethodStub>> := 
                    new HashMap<String, List<MethodStub>>()
            for m in bucket
                addToBucket(shortBuckets, strategy.shortName(cl, m), m)
            for (key, shortBucket) in shortBuckets {
                if shortBucket.length = 1
                    result[shortBucket[0]] := key
                else {
                    def mediumBuckets:Map<String, List<MethodStub>> := 
                            new HashMap<String, List<MethodStub>>()
                    for m in shortBucket {
                        addToBucket(mediumBuckets, strategy.mediumName(cl, m),
                                m)
                    }
                    for (mKey, mediumBucket) in mediumBuckets {
                        if mediumBucket.length = 1
                            result[mediumBucket[0]] := mKey
                        else {
                            for m in mediumBucket
                                result[m] := strategy.longName(cl, m)
                        }
                    }
                }
            }
        }
    }

    @private
    @class
    method addToBucket(buckets:Map<String, List<MethodStub>>, name:String, 
            m:MethodStub) {
        var bucket := buckets[name]
        if bucket = null {
            bucket := new Array<MethodStub>()
            buckets[name] := bucket
        }
        bucket.add(m)
    }
}