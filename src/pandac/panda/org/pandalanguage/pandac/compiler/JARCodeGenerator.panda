package org.pandalanguage.pandac.compiler

uses org.pandalanguage.pandac.stubs.MethodStub
uses org.pandalanguage.pandac.tree.MethodNode

class JARCodeGenerator (CodeGenerator) {
    constant PANDA_JAR := "PandaCoreClasses.jar"

    def program:Program

    def compilerSettings:CompilerSettings

    def jcg:JavaCodeGenerator

    constructor(p:Program, compilerSettings:CompilerSettings) {
        self.program := p
        self.compilerSettings := compilerSettings
        self.jcg := new JavaCodeGenerator(program, compilerSettings)
    }

    @override
    function defaultExtension():String {
        return ".jar"
    }

    method getMainClass():String? {
        for cl in program.classes.values {
            for m in cl.methods {
                if m.stub.name = MethodNode.MAIN_NAME
                    return jcg.getName(cl.stub)
            }
        }
        return null
    }

    method writeManifest(manifest:File, classPath:String) {
        var out := new BufferedOutputStream(manifest.openOutputStream())
        out.writeLine("Manifest-Version: 1.0")
        out.writeLine("Class-Path: " + classPath)
        var main := getMainClass()
        if main != null
            out.writeLine("Main-Class: " + main)
        out.close()
    }

    @private
    @pre(f.path.startsWith(prefix))
    method deleteTree(prefix:String, f:File) {
        try {
            if !f.exists()
                return
            if f.isDirectory() {
                var files := f.list()
                for file in files
                    deleteTree(prefix, file)
            }
            f.delete()
        }
        catch e:IOException {
        }
    }
    @post(!f.exists())

    method deleteTree(f:File) {
        deleteTree(f.path, f)
    }

    @private
    method listTree(f:File, suffix:String, result:List<File>) {
        if f.isDirectory() {
            def files := f.list()
            for file in files
                listTree(file, suffix, result)
        }
        else if f.name.endsWith(suffix)
            result.add(f)
    }

    method listTree(f:File, suffix:String):List<File> {
        var result := new Array<File>()
        listTree(f, suffix, result)
        return result
    }

    method copyJAR(relativePath:String, target:File, classPath:MutableString,
            manifestClassPath:MutableString) {
        def srcFile := PandaCompiler.PANDA_HOME.resolve(relativePath)
        def inStream := srcFile.openInputStream()
        def destFile := target.resolve(srcFile.name)
        def out := destFile.openOutputStream()
        inStream.sendTo(out)
        out.close()
        classPath.append(":" + srcFile.path)
        if manifestClassPath.length > 0
            manifestClassPath.append(" ")
        manifestClassPath.append(srcFile.name)
    }

    @override
    method generateCode(target:File) {
        def tmpDir := File.TEMP.resolve("pandaJavaTmp")
        deleteTree(tmpDir)
        def javaDir := tmpDir.resolve("src")
        jcg.generateCode(javaDir)
        def javaFiles := listTree(javaDir, ".java")
        def classPath := new MutableString(javaDir.path)
        def manifestClassPath := new MutableString()
        def parent := target.parent()
        if parent = null {
            throw new CompilerException("could not determine parent of " +
                    target)
        }
        copyJAR("java/core/lib/" + PANDA_JAR, parent, classPath,
                manifestClassPath)
        def classesDir := tmpDir.resolve("classes")
        classesDir.createDirectories()
        def modules := compilerSettings.modules
        for m in modules {
            for jar in m.javaJars
                copyJAR(jar, parent, classPath, manifestClassPath) 
        }
        def javac := compilerSettings.javac
        def commonParams := new Array<String>()
        commonParams.add("-d")
        commonParams.add(classesDir.absolutePath())
        commonParams.add("-cp")
        commonParams.add(classPath->>String)
        commonParams.add("-source")
        commonParams.add("1.8")
        -- Ideally, we'd just create a big command line containing the name of
        -- every file to compile. Unfortunately, we easily run afoul of command
        -- line length limits when we do that. Instead compile the files in
        -- batches.
        var params := new Array<String>(commonParams)
        constant BATCH_SIZE := 250
        var count := 0
        for f in javaFiles {
            var p := f.path
            assert p.startsWith(javaDir.path)
            p := p[javaDir.path.length + 1..]
            assert p.endsWith(".java")
            def target := classesDir.resolve(p[..p.length - ".java".length] +
                    ".class")
            if !target.exists() {
                params.add(p)
                count += 1
                if count = BATCH_SIZE {
                    System.exec(javac, javaDir, params...)
                    params := new Array<String>(commonParams)
                    count := 0
                }
            }
        }
        if count >= 1
            System.exec(javac, javaDir, params...)


        def manifest := tmpDir.resolve("manifest.mf")
        writeManifest(manifest, manifestClassPath->>String)
        def jar := compilerSettings.jar
        def jarParams := new Array<String>()
        jarParams.add("cfm")
        jarParams.add(target.absolutePath())
        jarParams.add(manifest.absolutePath())
        for f in classesDir.list() {
            var p := f.path
            assert p.startsWith(classesDir.path)
            p := p[classesDir.path.length + 1..]
            jarParams.add(p)
        }
        System.exec(jar, classesDir, jarParams...)

        if getMainClass() = null {
            new PLinkCodeGenerator(program, compilerSettings).generateCode(
                    parent.resolve(target.simpleName + ".plink"))
        }

        if !compilerSettings.preserveTempArtifacts
            deleteTree(tmpDir)        
    }
}