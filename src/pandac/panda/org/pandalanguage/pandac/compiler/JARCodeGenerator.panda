package org.pandalanguage.pandac.compiler

uses org.pandalanguage.pandac.tree.MethodNode
uses org.pandalanguage.pandac.tree.SymbolTable

class JARCodeGenerator (CodeGenerator) {
    constant PANDA_JAR := "PandaCoreClasses.jar"

    @override
    function defaultExtension():String {
        return ".jar"
    }

    method getMainClass():String? {
        var classes := SymbolTable.classes
        for i in 0 ... classes.length - 1 {
            var cl := classes[i]
            for j in 0 ... cl.length - 1 {
                var n := cl[j]
                if n-?>(MethodNode) & n->(MethodNode).name = MethodNode.MAIN_NAME
                    return new JavaCodeGenerator().getName(cl)
            }
        }
        return null
    }

    method writeManifest(manifest:File, classPath:String) {
        var out := new BufferedOutputStream(manifest.openOutputStream())
        out.writeLine("Manifest-Version: 1.0")
        out.writeLine("Class-Path: " + classPath)
        var main := getMainClass()
        if main != null
            out.writeLine("Main-Class: " + main)
        out.close()
    }

    @private
    @pre(f.path.startsWith(prefix))
    method deleteTree(prefix:String, f:File) {
        try {
            if !f.exists()
                return
            if f.isDirectory() {
                var files := f.list()
                for file in files
                    deleteTree(prefix, file)
            }
            f.delete()
        }
        catch e:IOException {
        }
    }
    @post(!f.exists())

    method deleteTree(f:File) {
        deleteTree(f.path, f)
    }

    @private
    method listTree(f:File, suffix:String, result:Array<File>) {
        if f.isDirectory() {
            var files := f.list()
            for i in 0 ... files.length - 1
                listTree(files[i], suffix, result)
        }
        else if f.name.endsWith(suffix)
            result.append(f)
    }

    method listTree(f:File, suffix:String):Array<File> {
        var result := new Array<File>()
        listTree(f, suffix, result)
        return result
    }

    method copyJAR(relativePath:String, target:File, classPath:MutableString,
            manifestClassPath:MutableString) {
        var srcFile := PandaCompiler.PANDA_HOME.resolve(relativePath)
        var inStream := srcFile.openInputStream()
        var destFile := target.resolve(srcFile.name)
        var out := destFile.openOutputStream()
        inStream.sendTo(out)
        out.close()
        classPath.append(":" + srcFile.path)
        if manifestClassPath.length > 0
            manifestClassPath.append(" ")
        manifestClassPath.append(srcFile.name)
    }

    @override
    method generateCode(target:File) {
        var tmpDir := File.TEMP.resolve("pandaJavaTmp")
        deleteTree(tmpDir)
        var javaDir := tmpDir.resolve("src")
        var jcg := new JavaCodeGenerator()
        jcg.generateCode(javaDir)
        var javaFiles := listTree(javaDir, ".java")
        var classPath := javaDir.path->>(MutableString)
        var manifestClassPath := new MutableString()
        var parent := target.parent()
        if parent = null {
            throw new CompilerException("could not determine parent of " +
                    target)
        }
        copyJAR("java/core/lib/" + PANDA_JAR, parent, classPath,
                manifestClassPath)
        var classesDir := tmpDir.resolve("classes")
        classesDir.createDirectories()
        var modules := CompilerSettings.modules
        for i in 0 ... modules.length - 1 {
            for j in 0 ... modules[i].javaJars.length - 1 {
                copyJAR(modules[i].javaJars[j], parent, classPath,
                        manifestClassPath) 
            }
        }
        var javac := CompilerSettings.javac
        var params := new Array<String>("-d", classesDir.absolutePath(), "-cp", 
                classPath->>(String), "-source", "1.8")
        for i in 0 ... javaFiles.length - 1 {
            var p := javaFiles[i].path
            assert p.startsWith(javaDir.path)
            p := p[javaDir.path.length + 1..]
            params.append(p)
        }
        System.exec(javac, javaDir, params...)

        var manifest := tmpDir.resolve("manifest.mf")
        writeManifest(manifest, manifestClassPath->>(String))
        var jar := CompilerSettings.jar
        params := new Array<String>("cfm", target.absolutePath(),
                manifest.absolutePath())
        var classFiles := listTree(classesDir, ".class")
        for i in 0 ... classFiles.length - 1 {
            var p := classFiles[i].path
            assert p.startsWith(classesDir.path)
            p := p[classesDir.path.length + 1..]
            params.append(p)
        }
        System.exec(jar, classesDir, params...)

        if getMainClass() = null {
            new PLinkCodeGenerator().generateCode(parent.resolve(
                    target.simpleName + ".plink"))
        }

        if !CompilerSettings.preserveTempArtifacts
            deleteTree(tmpDir)        
    }
}