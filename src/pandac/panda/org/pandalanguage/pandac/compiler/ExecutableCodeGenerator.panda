package org.pandalanguage.pandac.compiler

uses org.pandalanguage.pandac.tree.ClassNode
uses org.pandalanguage.pandac.types.Type

class ExecutableCodeGenerator (CodeGenerator) {
    var libpanda:String?

    def program:Program

    def compilerSettings:CompilerSettings

    constructor(program:Program, compilerSettings:CompilerSettings) {
        self.program := program
        self.compilerSettings := compilerSettings
    }

    @override
    function defaultExtension():String {
        return ""
    }
    
    @override
    method generateCode(target:File) {
        -- compile ll file to asm using llc
        var parent := target.parent()
        if parent = null {
            throw new CompilerException("could not determine parent " +
                    "directory of '\{target}'")
        }
        var ll := parent.resolve(target.simpleName + ".ll")
        new LLVMCodeGenerator(program, compilerSettings).generateCode(ll)
        parent := ll.parent()
        if parent = null {
            throw new CompilerException("could not determine parent " +
                    "directory of '\{ll}'")
        }
        def asm := parent.resolve(ll.simpleName + ".s")
        def llcArgs := new Array<String>()
        for arg in StaticSettings.LLC_EXE_ARGS
            llcArgs.add(arg)
        llcArgs.add("-o")
        llcArgs.add(asm.path)
        llcArgs.add(ll.path)
        def FIXMEllcArgs := new PrimitiveArray<String>(llcArgs.length)
        for i, v in llcArgs
            FIXMEllcArgs[i] := v
        System.exec(PandaCompiler.getLLVMHome().resolve("llc"), FIXMEllcArgs...)
        if !asm.exists()
            throw new CompilerException("llc failure")
        if !compilerSettings.preserveTempArtifacts
            ll.delete()

        -- compile asm file
        def cppArgs := new Array<String>()
        cppArgs.add("-o")
        cppArgs.add(target.path)
        cppArgs.add(asm.path)
        def searchPaths := compilerSettings.librarySearchPaths
        for searchPath in searchPaths
            cppArgs.add("-L" + searchPath.path)
        def libraries := compilerSettings.libraries
        for lib in libraries {
            if lib.endsWith(".o")
                cppArgs.add(lib)
            else
                cppArgs.add("-l" + lib)
        }
        for arg in StaticSettings.GCC_EXE_ARGS
            cppArgs.add(arg)
        def objects := compilerSettings.objectFiles
        for obj in objects
            cppArgs.add(obj.path)
        def modules := compilerSettings.modules
        for module in modules {
            for arg in module.gccExeArgs {
                cppArgs.add(arg.replace("$PANDA_HOME", 
                        PandaCompiler.PANDA_HOME.path))
            }
        }

        -- almost everything is C, but we do have a bit of C++ stuff, so we use
        -- a C++ compiler to get default linkage with the C++ libraries
        Console.writeLine(cppArgs)
        def FIXMEcppArgs := new PrimitiveArray<String>(cppArgs.length)
        for i, v in cppArgs
            FIXMEcppArgs[i] := v
        System.exec(PandaCompiler.getCPPCompiler(), FIXMEcppArgs...)
        if !compilerSettings.preserveTempArtifacts
            asm.delete()
    }
}