package org.pandalanguage.pandac.compiler

uses org.pandalanguage.pandac.compiler.Message
uses org.pandalanguage.pandac.parser.ParseNode
uses org.pandalanguage.pandac.stubs.UntypedClassStub
uses org.pandalanguage.pandac.tree.Position
uses org.pandalanguage.pandac.types.ClassType
uses org.pandalanguage.pandac.types.Type

class TypeMap : Immutable {
    @private
    constant DUMMY_CONTEXT := new LookupContext(new Array<String>(), 
            new HashMap<String, String>())

    @private
    constant ALL_TYPES := "allTypes"

    @private
    def queue := new MessageQueue()

    constructor(stubs:CollectionView<UntypedClassStub>, errors:ErrorReporter) {
        def immutableStubs := new ImmutableArray<UntypedClassStub>(stubs)
        Thread.start(method() {
            def typeMap := new MutableTypeMap(immutableStubs, errors)
            loop {
                def message := queue.getMessage()
                if message-?>(Type) {
                    typeMap.putType(message->(Type))
                }
                else if message-?>((String, MessageQueue)) {
                    def command, replyTo := message->((String, MessageQueue))
                    assert command = ALL_TYPES
                    replyTo.post(typeMap.allTypes)
                }
                else {
                    def node, context, replyTo := 
                            message->((ParseNode, LookupContext?, 
                                MessageQueue))
                    def type := typeMap.getType(node, context)
                    replyTo.post(type)
                }
            }
        }, false)
    }

    @self
    method getType(node:ParseNode, lookupContext:LookupContext?):Type? {
        def replyTo := Thread.currentThread().queue
        queue.post((node, lookupContext, replyTo))
        def type := replyTo.getMessage()->(Type?)
        return type
    }

    method allTypes():ImmutableHashMap<String, Type> {
        def replyTo := Thread.currentThread().queue
        queue.post((ALL_TYPES, replyTo))
        return replyTo.getMessage()->(ImmutableHashMap<String, Type>)
    }

    @self
    method putType(type:Type) {
        queue.post(type)
    }
 }
