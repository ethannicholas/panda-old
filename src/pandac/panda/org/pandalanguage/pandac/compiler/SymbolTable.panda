package org.pandalanguage.pandac.compiler

uses org.pandalanguage.pandac.stubs.Methods
uses org.pandalanguage.pandac.stubs.Symbol
uses org.pandalanguage.pandac.tree.MethodNode

class SymbolTable (Iterable<Symbol>) {
    @protected
    def parents:ListView<SymbolTable>

    def mySymbols := OrderedMap<String, Symbol>()

    init() {
        parents := []
    }

    init(symbols:MapView<String, Symbol>) {
        parents := []
        self.mySymbols.addAll(symbols)
    }

    init(parent:SymbolTable) {
        self.parents := [parent]
    }    

    init(parents:ListView<SymbolTable>) {
        self.parents := parents[..]
    }    

    init(parents:ListView<SymbolTable>, 
            symbols:OrderedMap<String, Symbol>) {
        self.parents := parents[..]
        self.mySymbols.addAll(symbols)
    }    

    function [](name:String):Symbol? {
        var result := mySymbols[name]
        if result != null & result-!>Methods {
            return result
        }
        if name = MethodNode.INIT_NAME {
            return result
        }
        for p in parents {
            if result-?>Methods {
                def prev := p[name]
                if prev-?>Methods {
                    result := prev->Methods + result->Methods
                }
            }
            else {
                assert result = null
                result := p[name]
                if result != null & result-!>Methods {
                    return result
                }
            }
        }
        return result
    }

    @self
    method add(name:String, s:Symbol) {
        assert name != "_"
        if s-?>Methods {
            var methods := s->Methods
            def existing := self[name]
            if existing-?>Methods {
                var oldMethods := existing->Methods
                oldMethods += methods
                methods := oldMethods
            }
            mySymbols[name] := methods
        }
        else {
            mySymbols[name] := s
        }
    }

    @self
    method add(s:Symbol) {
        add(s.name, s)
    }

    method addAll(map:MapView<String, Symbol>) {
        for (name, s) in map {
            add(name, s)
        }
    }

    @override
    function get_iterator():Iterator<Symbol> {
        def keys := HashSet<String>()
        for p in parents {
            for s in p {
                keys.add(s.name)
            }
        }
        for key in mySymbols.keys {
            keys.add(key)
        }
        def result := Array<Symbol>()
        for key in keys {
            result.add(self[key])
        }
        return result.iterator
    }

    @override
    function convert():String {
        return "SymbolTable<symbols=\{mySymbols}, parents=\{parents}>"
    }
}