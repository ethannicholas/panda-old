package org.pandalanguage.pandac.compiler

uses org.pandalanguage.pandac.stubs.Symbol

class SymbolTable {
    def parent:SymbolTable?

    @private
    def symbols := new HashMap<String, Symbol>()

    constructor() {
        self.constructor(null)
    }

    constructor(parent:SymbolTable?) {
        self.parent := parent
    }

    function [](name:String):Symbol? {
        def result := symbols[name]
        if result != null
            return result
        if parent != null
            return parent[name]
        return null
    }

    @self
    method []:=(name:String, s:Symbol) {
        --assert symbols[name] = null : "symbol '\{s}' was already defined"
        symbols[name] := s
    }

    @override
    function format(fmt:String):String {
        if parent != null
            return "SymbolTable<symbols=\{symbols}, parent=\{parent}>"
        else
            return "SymbolTable<symbols=\{symbols}>"
    }
}