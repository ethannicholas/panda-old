package org.pandalanguage.pandac.compiler

uses org.pandalanguage.pandac.tree.Variable

class SymbolTable {
    def parent:SymbolTable?

    @private
    def symbols := new HashMap<String, Variable>()

    constructor() {
        self.constructor(null)
    }

    constructor(parent:SymbolTable?) {
        self.parent := parent
    }

    function [](name:String):Variable? {
        def result := symbols[name]
        if result != null
            return result
        if parent != null
            return parent[name]
        return null
    }

    @self
    method []:=(name:String, v:Variable) {
        symbols[name] := v
    }

    @override
    function format(fmt:String):String {
        if parent != null
            return "SymbolTable<symbols=\{symbols}, parent=\{parent}>"
        else
            return "SymbolTable<symbols=\{symbols}>"
    }
}