package org.pandalanguage.pandac.types

================================================================================
A type with generic parameters, such as 
`panda.collections.HashMap<panda.core.String, panda.core.Object>`. See
`GenericParameterType` for a more thorough discussion of how types are handled
with respect to generics.
================================================================================
class GenericType : ClassType {
    def base:ClassType
    def parameters:ImmutableArray<Type>

    @private
    constructor(base:ClassType, parameters:ImmutableArray<Type>) {
        super.constructor(getName(base, parameters))
        self.base := base
        self.parameters := parameters
    }

    @private
    @class
    function getName(base:ClassType, parameters:ImmutableArray<Type>):String {
        var result := base.name->>(MutableString)
        result.append("<")
        for i, p in parameters {
            if i > 0
                result.append(",")
            result.append(p.name)
        }
        result.append(">")
        return result->>(String)
    }

    @private
    @override
    function baseName():String {
        return base.baseName
    }
}
