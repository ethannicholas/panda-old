package org.pandalanguage.pandac.types

uses org.pandalanguage.pandac.ast.ASTType
uses org.pandalanguage.pandac.compiler.CompilerException
uses org.pandalanguage.pandac.compiler.Context
uses org.pandalanguage.pandac.compiler.LookupContext
uses org.pandalanguage.pandac.compiler.OrderedMap
uses org.pandalanguage.pandac.compiler.PandaCompiler
uses org.pandalanguage.pandac.compiler.SymbolTable
uses org.pandalanguage.pandac.parser.PandaParser
uses org.pandalanguage.pandac.stubs.ClassStub
uses org.pandalanguage.pandac.stubs.FormalParameter
uses org.pandalanguage.pandac.stubs.MethodStub
uses org.pandalanguage.pandac.stubs.Methods
uses org.pandalanguage.pandac.stubs.Symbol
uses org.pandalanguage.pandac.stubs.TypeSymbol
uses org.pandalanguage.pandac.tree.Annotations
uses org.pandalanguage.pandac.tree.AnnotationType
uses org.pandalanguage.pandac.tree.FieldNode
uses org.pandalanguage.pandac.tree.MethodNode
uses org.pandalanguage.pandac.tree.MethodNodeType
uses org.pandalanguage.pandac.tree.Parameter
uses org.pandalanguage.pandac.tree.Position
uses org.pandalanguage.pandac.types.UnresolvedType

================================================================================
A concrete instantiation of a type with generic parameters, such as 
`panda.collections.HashMap<panda.core.String, panda.core.Object>`. See
`GenericParameterType` for a more thorough discussion of how types are handled
with respect to generics (note in particular that `HashMap<K, V>` is *not* a 
`GenericType`).
================================================================================
class GenericType : ClassType {
    def base:ClassType
    def parameters:ImmutableArray<Type>

    init(base:ClassType, 
            parameters:ListView<Type>) {
        super.init(getName(base, parameters))
        self.base := base
        self.parameters := ImmutableArray<Type>(parameters)
    }

    @private
    @class
    function getName(base:ClassType, parameters:ListView<Type>):String {
        def result := MutableString(base.name)
        result.append("<")
        for (i, p) in parameters.enumeration {
            if i > 0
                result.append(",")
            result.append(p.name)
        }
        result.append(">")
        return result.convert()
    }

    @override
    function get_simpleName():String {
        def result := MutableString(base.simpleName)
        result.append("<")
        for (i, p) in parameters.enumeration {
            if i > 0
                result.append(",")
            result.append(p.simpleName)
        }
        result.append(">")
        return result.convert()
    }    

    @private
    function parseType(name:String, context:Context):ASTType {
        return PandaParser(context.errors).parseType(name, 
                Position(File(name), -1))
    }

    @private
    method getType(type:Type, context:Context, 
            symbols:SymbolTable):Type {
        if type-?>UnresolvedType
            return type
        if type = VoidType.VOID
            return type
        def result := context.stubGenerator.resolveType(
                PandaParser(context.errors).parseType(type.name,
                    context.currentClass.position), symbols)
        if result != null
            return result
        throw CompilerException("unable to resolve type '\{type.name}'",
                Position.INTERNAL)
    }

    @private
    method instantiateField(f:FieldNode, context:Context, 
            symbols:SymbolTable):FieldNode {
        return FieldNode(f.position, self, f.name, f.annotations, f.kind,
                getType(f.type, context, symbols), f.initialValue, 
                f.tuplePosition)
    }

    @private
    method instantiateMethod(m:MethodStub, context:Context, 
            symbols:SymbolTable):MethodStub {
        def parameters := Array<FormalParameter>()
        for p in m.parameters {
            def type := getType(p.type, context, symbols)
            parameters.add(FormalParameter(p.position, p.name, type, 
                    p.isVar, p.isVarArg))
        }
        var annotations := m.annotations
        if m.body = null
            annotations += AnnotationType.ABSTRACT
        def result := MethodStub(m.position, m.name, annotations,
                m.methodType, parameters, getType(m.returnType, context,
                    symbols), 
                m.body, self)
        return result
    }

    @protected
    method addMethod(m:MethodStub, symbols:SymbolTable) {
        def methods := symbols.mySymbols[m.name]
        if methods = null
            symbols.add(Methods([m]))
        else if methods-?>Methods
            symbols.mySymbols[m.name] := methods->Methods + m
        else
            unreachable
    }

    method createStub(baseStub:ClassStub, parameterStubs:ListView<ClassStub?>, 
            external:Bit, context:Context):ClassStub {
        def baseParameters := baseStub.genericParameters
        assert baseParameters != null : 
                "base class (\{baseStub.name}) is not generic"
        assert baseParameters.count = parameters.count : 
                "parameter mismatch (\{baseParameters} vs. \{parameters})"
        def position := Position(File(name), -1)
        def symbols := SymbolTable()
        for (i, p) in parameters.enumeration {
            def symbol := TypeSymbol(p)
            symbols.add(baseParameters[i][0], symbol)
            def fullName := baseStub.name + "." + baseParameters[i][0]
            symbols.add(fullName, symbol)
        }        
        var superclass := baseStub.superclass
        if superclass != null {
            def resolved:Type?
            resolved := getType(superclass, context, symbols)
            superclass := resolved->ClassType
        }
        else
            superclass := null
        def interfaces := Array<ClassType>()
        for intf in baseStub.interfaces {
            var type:Type?
            type := getType(intf, context, symbols)
            if type != null {
                def intfClass := type.convert()->ClassType
                interfaces.add(intfClass)
            }
        }
        for f in baseStub.fields {
            if f.annotations.isClass | f.annotations.isThread
                symbols.add(f)
            else {
                def newField := instantiateField(f, context, symbols)
                symbols.add(newField)
            }
        }
        for m in baseStub.methods.filter(m => !m.annotations.isClass)
            addMethod(instantiateMethod(m, context, symbols), symbols)
        var annotations := baseStub.annotations - AnnotationType.UNSPECIFIED
        for (i, p) in parameters.enumeration {
            var unspecified := p-?>GenericParameterType
            def stub := parameterStubs[i]
            unspecified |= stub != null & stub.annotations.isUnspecified
            if unspecified {
                annotations += AnnotationType.UNSPECIFIED
                break
            }
        }
        return ClassStub(position, self, baseStub.classCategory, annotations, 
                superclass, interfaces, symbols.mySymbols, baseStub.lookupContext, 
                baseStub.invariants, null, external, null)
    }
}
