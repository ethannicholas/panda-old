package org.pandalanguage.pandac.types

uses org.pandalanguage.pandac.tree.MethodNode

================================================================================
Superclass of all "primitive" types, that is the `Int` types, the `Real` types,
`Bit`, and `Char`. These types are handled specially by the compiler and do not
have corresponding classes (although they are made to appear as if they do).
================================================================================
@abstract
class PrimitiveType : Type {
    @private
    def _size:Int

    @protected
    constructor(name:String, size:Int) {
        super.constructor(name)
        self._size := size
    }

    @override
    method findConverterTo(target:Type):MethodNode? {
        var result:MethodNode? := null
        if !target.isPrimitive {
            var cl := target.unwrapNullable.classNode
            result := findConverter(cl, self, target)
        }
        if result = null
            result := wrapperVersion().findConverterTo(target)
        return result
    }

    @override
    function simpleName():String {
        return name[name.lastIndexOf(".")->(Int) + 1..]
    }

    method wrapperVersion():WrapperType {
        return WrapperType.wrapperFor(self)
    }

    @override
    function unwrapRealLiteral():PrimitiveType {
        return self
    }

    @override
    function isPrimitive():Bit {
        return true
    }

    @override
    function size():Int {
        return _size
    }
}
