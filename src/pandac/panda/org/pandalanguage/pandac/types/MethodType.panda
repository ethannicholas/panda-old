package org.pandalanguage.pandac.types

uses org.pandalanguage.pandac.compiler.Context
uses org.pandalanguage.pandac.tree.ClassNode

================================================================================
Represents a method type, with a list of parameters and return type (which may
be `void`).
================================================================================
class MethodType : ClassType {
    def methodParameters:ImmutableArray<Type>
    
    def returnType:Type

    def isFunction:Bit

    def immutableMethod:Bit

    init(methodParameters:ListView<Type>, returnType:Type, 
            isFunction:Bit, isImmutable:Bit) {
        super.init(getName(methodParameters, returnType, isFunction,
                isImmutable))
        self.methodParameters := ImmutableArray<Type>(methodParameters)
        self.returnType := returnType
        self.isFunction := isFunction
        self.immutableMethod := isImmutable
    }

    @private
    @class
    function getName(parameterTypes:ListView<Type>, returnType:Type, 
            isFunction:Bit, isImmutable:Bit):String {
        var parameters := MutableString()
        for (i, p) in parameterTypes.enumeration {
            if i > 0
                parameters.append(",")
            parameters.append(p.name)
        }
        var yields:String
        if isFunction
            yields := "=>"
        else
            yields := "=&>"
        if isImmutable
            yields += "*"
        if returnType = VoidType.VOID
            return "(" + parameters + ")" + yields + "()"
        else
            return "(" + parameters + ")" + yields + "(" + returnType.name + ")"
    }

    @override
    function get_simpleName():String {
        def result := MutableString("(")
        for (i, p) in methodParameters.enumeration {
            if i > 0
                result.append(",")
            result.append(p.simpleName)
        }
        result.append(")=")
        if !isFunction
            result.append("&")
        result.append(">")
        if immutableMethod
            result.append("*")
        result.append("(")
        if returnType != VoidType.VOID
            result.append(returnType.simpleName)
        result.append(")")
        return result.convert()
    }

    @override
    function get_displayName():String {
        def result := MutableString("(")
        for (i, p) in methodParameters.enumeration {
            if i > 0
                result.append(", ")
            result.append(p.displayName)
        }
        result.append(")=")
        if !isFunction
            result.append("&")
        result.append(">")
        if immutableMethod
            result.append("*")
        result.append("(")
        if returnType != VoidType.VOID
            result.append(returnType.displayName)
        result.append(")")
        return result.convert()
    }

    @override
    function get_isMethod():Bit {
        return true
    }

    @override
    method get_isImmutable(context:Context):Bit {
        return immutableMethod
    }

    @override
    @unsafeFunction
    function implicitCastCost(context:Context, var target:Type):Int? {
        if !immutableMethod & target.isImmutable(context)
            return null
        var result := 0
        if target.isNullable {
            target := target.unwrapNullable
            result += NullableType.TO_NULLABLE_CAST_COST
        }
        if self = target
            return result
        if target.isMethod {
            def m := target->MethodType
            if m.isFunction & !isFunction
                return null
            else if isFunction & !m.isFunction
                result += 1
            if methodParameters.count != m.methodParameters.count
                return null
            for (i, p) in methodParameters.enumeration {
                def cost := m.methodParameters[i].implicitCastCost(context, p)
                if cost = null
                    return null
                result += cost
            }
            def cost := returnType.implicitCastCost(context, m.returnType)
            if cost = null
                return null
            return result + cost
        }
        return self.convert()->ClassType.implicitCastCost(context, target)
    }

    @override
    function convert():MethodType {
        return self
    }

    @override
    function convert():ClassType {
        if immutableMethod
            return ClassType.METHOD
        else
            return ClassType.MUTABLE_METHOD
    }
}
