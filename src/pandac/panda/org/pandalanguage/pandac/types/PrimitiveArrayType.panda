package org.pandalanguage.pandac.types

uses org.pandalanguage.pandac.ast.ASTAnnotation
uses org.pandalanguage.pandac.ast.ASTBlock
uses org.pandalanguage.pandac.ast.ASTFile
uses org.pandalanguage.pandac.ast.ASTIdentifier
uses org.pandalanguage.pandac.ast.ASTInvariant
uses org.pandalanguage.pandac.ast.ASTReturn
uses org.pandalanguage.pandac.ast.ASTSimpleAnnotation
uses org.pandalanguage.pandac.ast.ASTStatement
uses org.pandalanguage.pandac.compiler.IRGenerator
uses org.pandalanguage.pandac.compiler.CompilerException
uses org.pandalanguage.pandac.compiler.Context
uses org.pandalanguage.pandac.compiler.LookupContext
uses org.pandalanguage.pandac.compiler.OrderedMap
uses org.pandalanguage.pandac.compiler.PandaCompiler
uses org.pandalanguage.pandac.compiler.StubGenerator
uses org.pandalanguage.pandac.compiler.SymbolTable
uses org.pandalanguage.pandac.parser.ParseNode
uses org.pandalanguage.pandac.parser.PandaParser
uses org.pandalanguage.pandac.stubs.ClassStub
uses org.pandalanguage.pandac.stubs.FormalParameter
uses org.pandalanguage.pandac.stubs.MethodStub
uses org.pandalanguage.pandac.stubs.Methods
uses org.pandalanguage.pandac.stubs.Symbol
uses org.pandalanguage.pandac.tree.Annotations
uses org.pandalanguage.pandac.tree.AnnotationType
uses org.pandalanguage.pandac.tree.BitLiteral
uses org.pandalanguage.pandac.tree.Block
uses org.pandalanguage.pandac.tree.Cast
uses org.pandalanguage.pandac.tree.CastType
uses org.pandalanguage.pandac.tree.ClassNode
uses org.pandalanguage.pandac.tree.FieldNode
uses org.pandalanguage.pandac.tree.FieldReference
uses org.pandalanguage.pandac.tree.IntegerLiteral
uses org.pandalanguage.pandac.tree.LocalVariable
uses org.pandalanguage.pandac.tree.MethodCall
uses org.pandalanguage.pandac.tree.MethodNode
uses org.pandalanguage.pandac.tree.MethodNodeType
uses org.pandalanguage.pandac.tree.Parameter
uses org.pandalanguage.pandac.tree.Position
uses org.pandalanguage.pandac.tree.Return
uses org.pandalanguage.pandac.tree.Statement
uses org.pandalanguage.pandac.tree.Value
uses org.pandalanguage.pandac.tree.ValueStatement
uses org.pandalanguage.pandac.tree.VariableAssignment
uses org.pandalanguage.pandac.tree.VariableReference
uses org.pandalanguage.pandac.tree.VariableType
uses org.pandalanguage.pandac.types.IntType

================================================================================
A primitive array (mutable or immutable) of another type.
================================================================================
class PrimitiveArrayType : GenericType {
    -- all statically-allocated types must be added to the MutableTypeMap 
    -- init
    constant IMMUTABLE_CHAR_ARRAY := PrimitiveArrayType(
            ClassType.IMMUTABLE_PRIMITIVE_ARRAY, CharType.CHAR)

    @pre(arrayClass = ClassType.PRIMITIVE_ARRAY | 
            arrayClass = ClassType.IMMUTABLE_PRIMITIVE_ARRAY)
    init(arrayClass:ClassType, elementType:Type) {
        super.init(arrayClass, FIXMEwrap(elementType))
    }

    -- ugh I can't wait until I have the array syntax wired up properly
    function FIXMEwrap(t:Type):Array<Type> {
        def a := Array<Type>()
        a.add(t)
        return a
    }

    @override
    function get_simpleName():String {
        -- FIXME we should be using elementType.simpleName here, but leaving it
        -- as-is for backwards compatibility until new compiler is finished
        return base.simpleName + "<" + elementType.name + ">"
    }

    property elementType:Type

    @private
    function get_elementType():Type {
        return parameters[0]
    }

    @override
    function get_isPrimitiveArray():Bit {
        return true
    }

    method createStub(unspecified:Bit, external:Bit, 
            context:Context):ClassStub {
        def position := Position(File(name), -1)
        def final := [AnnotationType.FINAL]
        def length := FieldNode(position, self, "$length", 
                Annotations(position, final), VariableType.DEF, 
                IntType.INT32, null, null)
        def maxLength := FieldNode(position, self, "$maxLength",
                Annotations(position, final), VariableType.DEF, 
                 IntType.INT32, null, null)
        def elements := FieldNode(position, self, FieldNode.ELEMENTS_NAME, 
                Annotations(position, final), VariableType.DEF,
                elementType, null, null)
        def symbols := SymbolTable()
        symbols.add(length)
        symbols.add(maxLength)
        symbols.add(elements)
        def classAnnotations := Array<AnnotationType>()
        if unspecified
            classAnnotations.add(AnnotationType.UNSPECIFIED)

        def lengthParameters := Array<FormalParameter>()
        def lengthStatements := Array<ASTStatement>()
        lengthStatements.add(ASTReturn(position, ASTIdentifier(position,
                length.name)))
        def lengthBody := ASTBlock(position, lengthStatements)
        def lengthFunction := MethodStub(position, "length", 
                Annotations(position, Array<AnnotationType>()),
                MethodNodeType.FUNCTION, Array<FormalParameter>(),
                IntType.INT32, lengthBody, self)
        addMethod(lengthFunction, symbols)

        def annotations := Array<ASTAnnotation>()
        annotations.add(ASTSimpleAnnotation(position, "synthetic"))
        annotations.add(ASTSimpleAnnotation(position, "safeReturn"))
        def parser := PandaParser(FatalErrorReporter())
        def immutable := 
                name.startsWith(ImmutablePrimitiveArray.name)
        def mutableVersion:String
        if immutable {
            mutableVersion := PrimitiveArray.name + 
                    name[name.indexOf("<")->Int..]
        }
        else
            mutableVersion := name
        def convert:String
        if immutable
            convert := ".convert()->\{name}"
        else
            convert := ""
        def sle1 := parser.parseMethodDeclaration(
                "function [..](left:panda.core.Int32," +
                "right:panda.core.Int32):\{name} {\n" +
                "var result := \{mutableVersion}(right - left)\n" +
                "for (i, v) in (left .. right).enumeration()\n" +
                "    result[i] := self[v]\n" +
                "return result\{convert}\n" +
                "}", null, annotations, position)
        assert sle1 != null
        addMethod(context.stubGenerator.convertMethod(false, sle1, self, 
                symbols)->MethodStub, symbols)

        def sle2 := parser.parseMethodDeclaration(
                "function [..](rawLeft:panda.core.Int32?," +
                "rawRight:panda.core.Int32?):\{name} {\n" +
                "def left:panda.core.Int32\n" +
                "if rawLeft = null\n" +
                "    left := 0\n" +
                "else\n" +
                "    left := rawLeft\n" +
                "def right:panda.core.Int32\n" +
                "if rawRight = null\n" +
                "    right := length()\n" +
                "else\n" +
                "    right := rawRight\n" +
                "var result := \{mutableVersion}(right - left)\n" +
                "for (i, v) in (left .. right).enumeration()\n" +
                "    result[i] := self[v]\n" +
                "return result\{convert}\n" +
                "}", null, annotations, position)
        assert sle2 != null
        addMethod(context.stubGenerator.convertMethod(false, sle2, self, 
                symbols)->MethodStub, symbols)

        def sli1 := parser.parseMethodDeclaration(
                "function [...](left:panda.core.Int32," +
                "right:panda.core.Int32):\{name} {\n" +
                "var result := \{mutableVersion}(right - left + 1)\n" +
                "for (i, v) in (left ... right).enumeration()\n" +
                "    result[i] := self[v]\n" +
                "return result\{convert}\n" +
                "}", null, annotations, position)
        assert sli1 != null
        addMethod(context.stubGenerator.convertMethod(false, sli1, self, 
                symbols)->MethodStub, symbols)

        def sli2 := parser.parseMethodDeclaration(
                "function [...](rawLeft:panda.core.Int32?," +
                "rawRight:panda.core.Int32?):\{name} {\n" +
                "def left:panda.core.Int32\n" +
                "if rawLeft = null\n" +
                "    left := 0\n" +
                "else\n" +
                "    left := rawLeft\n" +
                "def right:panda.core.Int32\n" +
                "if rawRight = null\n" +
                "    right := length() - 1\n" +
                "else\n" +
                "    right := rawRight\n" +
                "var result := \{mutableVersion}(right - left + 1)\n" +
                "for (i, v) in (left ... right).enumeration()\n" +
                "    result[i] := self[v]\n" +
                "return result\{convert}\n" +
                "}", null, annotations, position)
        assert sli2 != null
        addMethod(context.stubGenerator.convertMethod(false, sli2, self, 
                symbols)->MethodStub, symbols)

        if !immutable {
            def immutableVersion := ImmutablePrimitiveArray.name + 
                    name[name.indexOf("<")->Int..]
            def cnv := parser.parseMethodDeclaration(
                "function convert():\{immutableVersion} {\n" +
                "    return \{immutableVersion}(self)\n" +
                "}", null, annotations, position)
            assert cnv != null
            addMethod(context.stubGenerator.convertMethod(false, cnv, self, 
                    symbols)->MethodStub, symbols)
        }

        def superclass:ClassType
        if immutable
            superclass := ClassType.IMMUTABLE_PRIMITIVE_ARRAY
        else
            superclass := ClassType.PRIMITIVE_ARRAY
        return ClassStub(position, self, false, 
                Annotations(position, classAnnotations), 
                superclass, Array<ClassType>(), symbols.mySymbols, 
                LookupContext(), Array<ASTInvariant>(), null, external,
                null)
    }

    @override
    function convert():PrimitiveArrayType {
        return self
    }
}
