package org.pandalanguage.pandac.types

================================================================================
The two `Real` types.
================================================================================
class RealType : PrimitiveType {
    constant REAL32 := new RealType(4)

    constant REAL64 := new RealType(8)
    
    @private
    constructor(size:Int) {
        super.constructor("panda.core.Real" + size * 8, size)
    }

    @override
    method canExplicitCastTo(target:Type):Bit {
        if target.isPrimitive
            return target.isNumber
        return wrapperVersion().canExplicitCastTo(target)
    }

    @override
    method implicitCastCost(target:Type):Int {
        if target.isReal {
            if target = RealLiteralType.REAL_LITERAL
                return 0
            if target->(PrimitiveType).size < size
                return NO_MATCH
            var s := size
            var result := 0
            while s < target->(PrimitiveType).size {
                s *= 2
                result += 1
            }
            return result
        }
        else if target.isWrapper {
            var result := implicitCastCost(target.unwrapWrapper)
            if result != NO_MATCH & target.isNullable
                result += NullableType.TO_NULLABLE_CAST_COST
            return result
        }
        if !target.isPrimitive
            return wrapperVersion().implicitCastCost(target)
        return NO_MATCH
    }

    @override
    function isReal():Bit {
        return true
    }

    @override
    function isNumber():Bit {
        return true
    }

    @override
    function isNumeric():Bit {
        return true
    }
}
