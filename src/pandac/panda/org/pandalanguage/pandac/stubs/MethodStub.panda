package org.pandalanguage.pandac.stubs

uses org.pandalanguage.pandac.ast.ASTBlock
uses org.pandalanguage.pandac.compiler.Context
uses org.pandalanguage.pandac.tree.Annotations
uses org.pandalanguage.pandac.tree.MethodNode
uses org.pandalanguage.pandac.tree.MethodNodeType
uses org.pandalanguage.pandac.tree.Parameter
uses org.pandalanguage.pandac.tree.Position
uses org.pandalanguage.pandac.types.ClassType
uses org.pandalanguage.pandac.types.MethodType
uses org.pandalanguage.pandac.types.Type
uses org.pandalanguage.pandac.types.VoidType

================================================================================
Represents a parsed but not-yet-compiled method.
================================================================================
class MethodStub : Immutable {
    def position:Position

    def name:String

    def annotations:Annotations

    def methodType:MethodNodeType

    def body:ASTBlock?

    def owner:ClassType

    def parameters:ImmutableArray<FormalParameter>

    def returnType:Type

    init(position:Position, name:String, annotations:Annotations,
            methodType:MethodNodeType, parameters:ListView<FormalParameter>, 
            returnType:Type, body:ASTBlock?, owner:ClassType) {
        self.position := position
        self.name := name
        self.annotations := annotations
        self.methodType := methodType
        self.body := body
        self.parameters := ImmutableArray<FormalParameter>(
                parameters)
        self.returnType := returnType
        self.owner := owner
    }

    ============================================================================
    Returns true if this method overrides `parent`.
    ============================================================================
    function isOverrideOf(parent:MethodStub):Bit {
        if parent.name != name
            return false
        def isClass := parent.annotations.isClass
        if isClass != annotations.isClass
            return false
        if parent.returnType != returnType
            return false
        def parentParams := parent.parameters
        if parentParams.count != parameters.count
            return false
        def start:Int
        for i in 0 .. parentParams.count {
            if parentParams[i].type != parameters[i].type
                return false
        }
        return true
    }

    ============================================================================
    True if the method is declared virtual (in other words, it needs to appear 
    in the class' VTABLE). This is false for class methods, final methods, and
    inits.
    ============================================================================
    property isDeclaredVirtual:Bit

    @private
    function get_isDeclaredVirtual():Bit {
        return !annotations.isClass & 
                !annotations.isFinal & 
                !annotations.isPrivate & 
                methodType != MethodNodeType.INIT
    }

    method type(context:Context):MethodType {
        def types := Array<Type>()
        for p in parameters
            types.add(p.type)
        def result := MethodType(types, returnType, 
                methodType = MethodNodeType.FUNCTION, true)
        return context.resolveType(result.name)->MethodType
    }

    @override
    function convert():String {
        def result := MutableString()
        switch methodType {
            case MethodNodeType.METHOD: result.append("method ")
            case MethodNodeType.FUNCTION: result.append("function ")
            case MethodNodeType.INIT: { -* do nothing *- }
            default: unreachable
        }
        result.append(name)
        result.append("(")
        var start:Int
        for (i, p) in parameters.enumeration {
            if i > 0
                result.append(", ")
            result.append(p.name)
            result.append(":")
            result.append(p.type.displayName)
            if p.isVarArg
                result.append("...")
        }
        result.append(")")
        if returnType != VoidType.VOID {
            result.append(":")
            result.append(returnType.displayName)
        }
        return result.convert()
    }

    method write(out:IndentedOutputStream) {
        out.write(self)
        if body != null {
            out.write(" ")
            body.write(out)
        }
    }
}