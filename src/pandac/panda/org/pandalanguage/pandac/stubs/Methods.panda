package org.pandalanguage.pandac.stubs

================================================================================
Represents a group of same-named method overloads.
================================================================================
class Methods : Immutable (Symbol) {
	property name:String

	def methods:ImmutableArray<MethodStub>

	constructor(methods:ListView<MethodStub>) {
		self._name := methods[0].name
		self.methods := new ImmutableArray<MethodStub>(methods)
	}

	function +(m:MethodStub):Methods {
		def result := new Array<MethodStub>()
		for old in methods {
			if !m.isOverrideOf(old)
				result.add(old)
		}
		result.add(m)
		return new Methods(result)
	}

	function +(m:Methods):Methods {
		-- FIXME brain-dead O(N^2) approach
		def result := new Array<MethodStub>()
		outer: for oldMethod in methods {
			for newMethod in m.methods {
				if newMethod.isOverrideOf(oldMethod)
					continue outer
			}
			result.add(oldMethod)
		}
		result.addAll(m.methods)
		return new Methods(result)
	}

	@override
	function format(fmt:String):String {
		return methods.format(fmt)
	}
}