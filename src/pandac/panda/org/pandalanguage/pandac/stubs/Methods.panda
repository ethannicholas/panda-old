package org.pandalanguage.pandac.stubs

================================================================================
Represents a group of same-named method overloads.
================================================================================
class Methods : Immutable (Symbol) {
	property name:String

	def methods:ImmutableArray<MethodStub>

	init(m:MethodStub) {
		self._name := m.name
		def a := Array<MethodStub>()
		a.add(m)
		self.methods := ImmutableArray<MethodStub>(a)
	}

	init(methods:ListView<MethodStub>) {
		self._name := methods[0].name
		self.methods := ImmutableArray<MethodStub>(methods)
	}

	function +(m:MethodStub):Methods {
		def result := Array<MethodStub>()
		for old in methods {
			if !m.isOverrideOf(old)
				result.add(old)
		}
		result.add(m)
		return Methods(result)
	}

	function +(m:Methods):Methods {
		-- FIXME brain-dead O(N^2) approach
		def result := Array<MethodStub>()
		outer: for oldMethod in methods {
			for newMethod in m.methods {
				if newMethod.isOverrideOf(oldMethod)
					continue outer
			}
			result.add(oldMethod)
		}
		result.addAll(m.methods)
		return Methods(result)
	}

	@override
	function convert():String {
		return methods.convert()
	}
}