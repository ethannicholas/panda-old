package org.pandalanguage.pandac.parser

enum ActionType {
	SHIFT,
	REDUCE,
	CUT,
	MULTIPLE,
	ACCEPT
}

class Action {
	def type:ActionType

	def target:Int

	def subactions:Array<Action>?

	init(type:ActionType, target:Int) {
		self.type := type
		self.target := target
	}

	init(subactions:List<Action>) {
		self.type := ActionType.MULTIPLE
		self.subactions := Array<Action>(subactions)
	}

	@override
	function =(o:Object):Bit {
		if o-?>Action {
			def a := o->Action
			if type != a.type | target != a.target
				return false
			if subactions != null {
				if subactions.length != a.subactions.length
					return false
				for (i, v) in subactions.enumeration() {
					if v != a.subactions[i]
						return false
				}
			}
			return true
		}
		return false
	}

	@override
	function hash():Int {
		return target 
	}

	@override
	function convert():String {
		switch type {
			case ActionType.SHIFT: return "Action(ActionType.SHIFT, \{target})"
			case ActionType.REDUCE: return "Action(ActionType.REDUCE, \{target})"
			case ActionType.CUT: return "Action(ActionType.CUT, \{target})"
			case ActionType.MULTIPLE: return "Action(\{subactions})"
			case ActionType.ACCEPT: return "Action(ActionType.ACCEPT, 0)"
		}
		unreachable
	}
}
