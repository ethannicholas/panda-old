class Brainfuck {
    var pointer := 0
    var tape:Array<Int8>
    
    method interpret(code:String) {
        tape := Array<Int8>()
        tape.add(0)
        var instr := 0
        while instr < code.length {
            switch code[instr] {
                case ">": {
                    pointer += 1
                    while pointer >= tape.count
                        tape.add(0)
                }
                case "<": pointer -= 1
                case "+": tape[pointer] += 1
                case "-": tape[pointer] -= 1
                case ".": Console.write(tape[pointer]->Char)
                case ",": {
                    var c := Console.read()
                    if c != null
                        tape[pointer] := c->Int8
                    else
                        tape[pointer] := 0
                }
                case "[": if tape[pointer] = 0 {
                    var l := 1
                    while l > 0 {
                        instr += 1
                        if code[instr] = "]"
                            l -= 1
                        else if code[instr] = "["
                            l += 1
                    }
                }
                case "]": if tape[pointer] != 0 {
                    var l := 1
                    while l > 0 {
                        instr -= 1
                        if code[instr] = "["
                            l -= 1
                        else if code[instr] = "]"
                            l += 1
                    }
                }
            }
            instr += 1
        }
    }
    
    @class
    method main() {
        Brainfuck().interpret(Console.inputStream.readAsString())
    }
}