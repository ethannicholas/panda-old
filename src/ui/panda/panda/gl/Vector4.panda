package panda.gl

class Vector4 : Value {
	def x:Real32
	def y:Real32
	def z:Real32
	def w:Real32

	init(x:Real32, y:Real32, z:Real32, w:Real32) {
		self.x := x
		self.y := y
		self.z := z
		self.w := w
	}

	property length:Real32

	@private
	function get_length():Real32 {
		return (x ^ 2 + y ^ 2 + z ^ 2 + w ^ 2).sqrt
	}

	function normalized():Vector4 {
		return self * 1 / length
	}

	function +(v:Vector4):Vector4 {
		return Vector4(x + v.x, y + v.y, z + v.z, w + v.w)
	}

	function -(v:Vector4):Vector4 {
		return Vector4(x - v.x, y - v.y, z - v.z, w - v.w)
	}

	function *(scale:Real32):Vector4 {
		return Vector4(x * scale, y * scale, z * scale, w * scale)
	}

	@class
	function *(scale:Real32, v:Vector4):Vector4 {
		return Vector4(v.x * scale, v.y * scale, v.z * scale, v.w * scale)
	}

	function /(scale:Real32):Vector4 {
		return Vector4(x / scale, y / scale, z / scale, w * scale)
	}

	function dot(v:Vector4):Real32 {
		return x * v.x + y * v.y + z * v.z + w * v.w
	}

	@override
	function convert():String {
		return "[\{x}, \{y}, \{z}, \{w}]"
	}
}